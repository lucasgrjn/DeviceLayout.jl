@testitem "PDK Tools" setup = [CommonTestSetup] begin
    # PDK
    quiet_test_output() do
        return SchematicDrivenLayout.generate_pdk("MyPDK"; dir=tdir, user="testuser")
    end
    pdkpath = joinpath(tdir, "MyPDK")
    using Pkg
    pdktoml = Pkg.TOML.parsefile(joinpath(pdkpath, "Project.toml"))
    @test VersionNumber(pdktoml["compat"]["DeviceLayout"]).major == 1
    @test pdktoml["preferences"]["DeviceLayout"]["units"] == DeviceLayout.unit_preference

    quiet_test_output() do
        Pkg.develop(path=pdkpath)
        @eval using MyPDK
    end

    # Component package
    quiet_test_output() do
        SchematicDrivenLayout.without_precompile() do
            SchematicDrivenLayout.generate_component_package(
                "MyComps",
                MyPDK,
                user="testuser"
            )
            @test ENV["JULIA_PKG_PRECOMPILE_AUTO"] == "0" # Environment variable is not changed
        end
    end
    @test !haskey(ENV, "JULIA_PKG_PRECOMPILE_AUTO") # Temporary env var was removed
    comppkg = joinpath(pdkpath, "components", "MyComps")
    @test isfile(joinpath(comppkg, "test", "runtests.jl")) # Package template includes tests
    quiet_test_output() do
        return Pkg.develop(path=comppkg)
    end

    # Component file
    SchematicDrivenLayout.generate_component_definition(
        "MyComposite",
        MyPDK,
        joinpath(comppkg, "src", "MyComposites.jl");
        composite=true
    )
    @test isfile(joinpath(comppkg, "src", "MyComposites.jl")) # File was generated
    quiet_test_output() do
        Pkg.rm("MyComps")
        return Pkg.rm("MyPDK")
    end
end
