"""
    abstract type GeometryEntityStyle

A style that can be used with a `GeometryEntity` to create a modified entity.

May use `(sty::MyStyle)(ent)`, `styled(ent, sty)`, or
`MyStyle(ent, style_args...; style_kwargs...)` to create a `StyledEntity`.

A `GeometryEntityStyle` should implement `to_polygons(::MyEntity, ::MyStyle; kwargs...)` for
any entity type it can be applied to. As a fallback, it can implement
`to_polygons(::Polygon, ::MyStyle; kwargs...)`, in which case entities will be converted to
polygons before applying the style.

Unless implemented by the style, `lowerleft` and `upperright` (and hence `bounds`)
of a styled entity will use the underlying entity's bounds. Similarly, `footprint` and
`halo` will fall back to using the underlying entity. Exceptions include the `NoRender`
style, in which case the entity is treated as a zero-area `Rectangle`
(ignored in collective `bounds` calculations), as well as `OptionalStyle`, in which
case the default style is used (in case it has special behavior).
"""
abstract type GeometryEntityStyle end

Base.broadcastable(x::GeometryEntityStyle) = Ref(x)

"""
    StyledEntity{T, U <: GeometryEntity{T}, S <: GeometryEntityStyle} <: GeometryEntity

`GeometryEntity` composing another `GeometryEntity` with a `GeometryEntityStyle`.

The use of a `StyledEntity` allows the composition of operations like rounding on geometric
entities without committing to a particular representation of those entities.
"""
struct StyledEntity{T, U <: GeometryEntity{T}, S <: GeometryEntityStyle} <:
       GeometryEntity{T}
    ent::U
    sty::S
end

Base.convert(::Type{GeometryEntity{T}}, e::StyledEntity) where {T} =
    StyledEntity(convert(GeometryEntity{T}, e.ent), e.sty)
Base.copy(ent::StyledEntity) = styled(ent.ent, ent.sty)

Base.show(io::IO, e::StyledEntity) = print(io, e.ent, " styled as ", e.sty)
function Base.show(io::IO, mime::MIME"text/plain", e::StyledEntity)
    show(io, mime, e.ent)
    print(io, "\n  style: ")
    return show(io, e.sty)
end

"""
    style(styled_ent::StyledEntity)

Return the `GeometryEntityStyle` of `styled_ent`.
"""
style(styled_ent::StyledEntity) = styled_ent.sty
style(::GeometryEntity) = Plain()

"""
    entity(styled_ent::StyledEntity)

Return the `GeometryEntity` styled by `styled_ent`.
"""
entity(styled_ent::StyledEntity) = styled_ent.ent
entity(ent::GeometryEntity) = ent

"""
    styled(ent, sty)

Return `StyledEntity(ent, sty)`.
"""
styled(ent, sty) = StyledEntity(ent, sty)
(s::GeometryEntityStyle)(ent::GeometryEntity) = StyledEntity(ent, s)
(T::Type{<:GeometryEntityStyle})(x::GeometryEntity, args...; kwargs...) =
    styled(x, T(args...; kwargs...))

"""
    unstyled(styled_ent::StyledEntity)

Return the unstyled entity referenced by `styled_ent`.

If `styled_ent.ent` is itself a `StyledEntity`, apply `unstyled` recursively until
the original plain `GeometryEntity` is found.
"""
unstyled(styled_ent::StyledEntity) = unstyled(styled_ent.ent)
unstyled(ent::GeometryEntity) = ent

"""
    unstyled_type(::GeometryEntity)
    unstyled_type(::Type{GeometryEntity})

Return the type of the unstyled entity beneath all styles.
"""
unstyled_type(::Type{StyledEntity{T, U, V}}) where {T, U, V} = unstyled_type(U)
unstyled_type(::Type{T}) where {T <: GeometryEntity} = T
unstyled_type(::T) where {T <: GeometryEntity} = unstyled_type(T)

lowerleft(ent::StyledEntity{T, U, V}) where {T, U, V} = lowerleft(ent.ent)
upperright(ent::StyledEntity{T, U, V}) where {T, U, V} = upperright(ent.ent)
footprint(ent::StyledEntity{T, U, V}) where {T, U, V} = footprint(ent.ent)
halo(ent::StyledEntity{T, U, V}, outer_delta, inner_delta=nothing) where {T, U, V} =
    halo(ent.ent, outer_delta, inner_delta)

"""
    to_polygons(styled_ent::StyledEntity)

Return an unstyled `Polygon` or Vector{<:Polygon} resulting from the application of styles.
"""
function to_polygons(styled_ent::StyledEntity; kwargs...)
    return to_polygons(styled_ent.ent, styled_ent.sty; kwargs...)
end

# If a style has no specialization for `ent`, convert `ent` to polygons first
function to_polygons(ent::GeometryEntity, sty::GeometryEntityStyle; kwargs...)
    return to_polygons.(to_polygons(ent; kwargs...), sty; kwargs...)
end

# default no transform
transform(sty::GeometryEntityStyle, f::Transformation) = sty
transform(ent::StyledEntity, f::Transformation) =
    StyledEntity(f(ent.ent), transform(ent.sty, f))

###### Generic styles
"""
    Plain <: GeometryEntityStyle

Plain style. Does not affect rendering of the styled entity.
"""
struct Plain <: GeometryEntityStyle end
to_polygons(ent::GeometryEntity, ::Plain; kwargs...) = to_polygons(ent; kwargs...)
Base.show(io::IO, ::Plain) = print(io, "Plain()")

"""
    NoRender <: GeometryEntityStyle

Style that marks an entity to be skipped when rendering.

`NoRender`-styled entities have zero-area `bounds` and `footprint` and empty `halo`.
"""
struct NoRender <: GeometryEntityStyle end
to_polygons(::GeometryEntity{T}, ::NoRender; kwargs...) where {T} = Polygon{T}[]
Base.show(io::IO, ::NoRender) = print(io, "NoRender()")
lowerleft(::StyledEntity{T, U, NoRender}) where {T, U} = zero(Point{T})
upperright(::StyledEntity{T, U, NoRender}) where {T, U} = zero(Point{T})
footprint(ent::StyledEntity{T, U, NoRender}) where {T, U} = bounds(ent)
halo(::StyledEntity{T, U, NoRender}, outer_delta, inner_delta=nothing) where {T, U} =
    Polygon{T}[]

"""
    struct MeshSized{T, S} <: GeometryEntityStyle where {T, S <: Real}
        h::T
        α::S
    end

Style that annotates a GeometryEntity with a mesh size to use in SolidModel rendering. The
generated mesh will include a size field defined as:

``s = h * max(s_g, (d/h)^α)``

where ``s`` is the mesh size, ``d`` is the distance away from the styled entity, ``s_g`` is
the global mesh scale parameter specified in [`DeviceLayout.SolidModels.mesh_scale`](@ref),
and `h` and `α` are the parameters provided. A smaller value of `h` will give a finer mesh
attached to the styled entity, and a larger value of `α` will give a more rapid increase in
size away from the styled entity.

If `α < 0`, the resulting size field will use
[`DeviceLayout.SolidModels.mesh_grading_default`](@ref). It is generally recommended to use
this global value in most scenarios, as it provides a helpful mechanism for global mesh
modification.

Each `MeshSized` entity generates a corresponding set of
[`DeviceLayout.SolidModels.mesh_control_points`](@ref) during the call to
[`DeviceLayout.SolidModels.render!`](@ref) along the perimeter of the styled entity. Exact
circular arcs also generate radius-sized points at their curvature centers unless
`curvature_sizing=false` is passed to `render!`. When geometry is extruded with a postrender
`extrude_z!` operation, its perimeter and curvature controls are repeated at requested
extrusion mesh layers, or at intervals no larger than each control's target size when no
layers are specified. Keeping the general entity sizing controls on boundaries avoids
overrefinement in surface regions that can be handled by adaptive mesh
refinement. Increasing resolution of entity edges can be achieved by reducing `h` on the
particular entity or ``s_g`` globally.

For the set of all [`DeviceLayout.SolidModels.mesh_control_points`](@ref) computed, a
corresponding mesh size will be computed, and the resulting mesh size is then the minimum
over all of these values

``s(x) = \\min_{x_p ∈ X_p} h_p \\max(s_g, \\frac{\\| x - x_p \\|_2}{h_p}^{α_p})``

where ``s(x)`` explicitly expresses that the size is a function of position ``x``, the ``p``
subscript denotes parameters associated to a single control ``x_p`` from the ``X_p`` of all
control points. Reducing ``h_p`` will result in a finer mesh next to an entity which will
then grow as a function of distance from the control point ``x_p``. In the implementation a
`KDTree` ordering per `(h,α)` pair is used to ensure the reduction over ``X_p`` is highly
efficient.

See also [`meshsized_entity`](@ref).

Associated methods:

  - [`DeviceLayout.SolidModels.mesh_scale`](@ref) for adjusting the `s_g` parameter.
  - [`DeviceLayout.SolidModels.mesh_grading_default`](@ref) for adjusting the value of `α`
    that a `MeshSized` with `α < 0` will map to.
  - [`DeviceLayout.SolidModels.reset_mesh_control!`](@ref) for resetting the `s_g` and
    `α_default` values to the default values.
  - [`DeviceLayout.SolidModels.add_mesh_size_point`](@ref) for adding a mesh sizing point (a
    point from which distance `d` will be calculated), and used in calculating any size
    fields.
  - [`DeviceLayout.SolidModels.mesh_control_points`](@ref) for access to the global dictionary
    of control points for which `d` and ultimately the mesh size will be calculated.
"""
struct MeshSized{T, S} <: GeometryEntityStyle where {T <: Coordinate, S <: Real}
    h::T
    α::S
    MeshSized(h::T, α::S=-1.0) where {T <: Coordinate, S <: Real} = new{T, S}(h, α) # Silence Aqua
end

function Base.show(io::IO, s::MeshSized)
    print(io, "MeshSized(", s.h)
    s.α == -1.0 || print(io, ", α=", s.α)
    return print(io, ")")
end

"""
    meshsized_entity(ent::GeometryEntity, h::T, α::S=-1.0) where {T, S <: Real}

Create a [`MeshSized`](@ref) entity, specifying a mesh size use in SolidModel rendering. The
generated mesh will include a size field defined as:

```
mesh size = h * max(s_g, (d/h)^α)
```

where d is the distance away from the styled entity, and `s_g` is the global mesh scale
parameter specified in [`DeviceLayout.SolidModels.mesh_scale`](@ref). A smaller value of `h` will give a finer mesh
attached to the styled entity, and a larger value of `α` will give a more rapid increase in
size away from the styled entity.

If `α < 0`, the size field will use [`DeviceLayout.SolidModels.mesh_grading_default`](@ref) used in rendering.
"""
meshsized_entity(ent::GeometryEntity, h::T, α::S=-1.0) where {T, S <: Real} =
    MeshSized(h, α)(ent)
to_polygons(ent::GeometryEntity, ::MeshSized; kwargs...) = to_polygons(ent; kwargs...)

"""
    struct OptionalStyle <: GeometryEntityStyle
        true_style::GeometryEntityStyle
        false_style::GeometryEntityStyle
        flag::Symbol
        default::Bool
    end
    OptionalStyle(true_style::GeometryEntityStyle, flag::Symbol;
        false_style::GeometryEntityStyle=Plain(), default::Bool=true)

Style that depends on a Boolean rendering option `flag` with default `default`.

`lowerleft`, `upperright`, `bounds`, `footprint`, and `halo` are forwarded to the
underlying entity styled with the default style.

# Examples

```julia
sty = OptionalStyle(Rounded(1μm), :rounding)
p = Rectangle(4μm, 4μm)
rounded_rect = to_polygons(sty(p))
plain_rect = to_polygons(sty(p), rounding=false)
```
"""
struct OptionalStyle <: GeometryEntityStyle
    true_style::GeometryEntityStyle
    false_style::GeometryEntityStyle
    flag::Symbol
    default::Bool
    OptionalStyle(a::GeometryEntityStyle, b, c, d) = new(a, b, c, d) # Silence Aqua
end
function OptionalStyle(
    true_style::GeometryEntityStyle,
    flag::Symbol;
    false_style::GeometryEntityStyle=Plain(),
    default::Bool=true
)
    return OptionalStyle(true_style, false_style, flag, default)
end

function Base.show(io::IO, s::OptionalStyle)
    print(io, "OptionalStyle(", s.true_style, ", ", repr(s.flag))
    s.false_style isa Plain || print(io, ", false_style=", s.false_style)
    s.default || print(io, ", default=false")
    return print(io, ")")
end

function to_polygons(ent::GeometryEntity, opt::OptionalStyle; kwargs...)
    sty = get(kwargs, opt.flag, opt.default) ? opt.true_style : opt.false_style
    return to_polygons(ent, sty; kwargs...)
end

function transform(sty::OptionalStyle, f::Transformation)
    return OptionalStyle(
        transform(sty.true_style, f),
        sty.flag,
        false_style=transform(sty.false_style, f),
        default=sty.default
    )
end

default_style(sty::OptionalStyle) = sty.default ? sty.true_style : sty.false_style

# Apply default style for interface functions
function lowerleft(ent::StyledEntity{T, U, OptionalStyle}) where {T, U}
    return lowerleft(default_style(ent.sty)(ent.ent))
end
function upperright(ent::StyledEntity{T, U, OptionalStyle}) where {T, U}
    return upperright(default_style(ent.sty)(ent.ent))
end
function footprint(ent::StyledEntity{T, U, OptionalStyle}) where {T, U}
    return footprint(default_style(ent.sty)(ent.ent))
end
function halo(
    ent::StyledEntity{T, U, OptionalStyle},
    outer_delta,
    inner_delta=nothing
) where {T, U}
    return halo(default_style(ent.sty)(ent.ent), outer_delta, inner_delta)
end

"""
    optional_entity(ent::GeometryEntity, flag::Symbol;
        true_style::GeometryEntityStyle=Plain(), default=true)

Return an entity to be rendered or not based on the rendering option `flag`.

# Example

```julia
julia> c = Cell();

julia> ent = optional_entity(Rectangle(2, 2), :optional_entities; default=false);

julia> render!(c, ent);

julia> length(elements(c))
0

julia> render!(c, ent; optional_entities=true);

julia> length(elements(c))
1
```
"""
optional_entity(
    ent::GeometryEntity,
    flag::Symbol;
    true_style::GeometryEntityStyle=Plain(),
    default=true
) = OptionalStyle(true_style, NoRender(), flag, default)(ent)

"""
    struct ToTolerance{T<:Coordinate} <: GeometryEntityStyle
        atol::T
    end

Style for rendering an entity to absolute tolerance `atol`.

Equivalent to passing or overriding the keyword `atol` when rendering this entity.
"""
struct ToTolerance{T <: Coordinate} <: GeometryEntityStyle
    atol::T
    ToTolerance(atol::T) where {T <: Coordinate} = new{T}(atol) # Silence Aqua
end
to_polygons(ent::GeometryEntity, sty::ToTolerance; kwargs...) =
    to_polygons(ent; merge((; kwargs...), (; atol=sty.atol))...)
Base.show(io::IO, s::ToTolerance) = print(io, "ToTolerance(", s.atol, ")")

"""
    struct WithDirection <: GeometryEntityStyle
        direction::typeof(1.0°)
    end
    WithDirection(direction=0°)

Style that annotates a `GeometryEntity` with a direction angle (CCW from the +X axis
in the entity's local frame) for use in simulation configuration. For example, a
lumped-port rectangle can carry its electrical orientation so that Palace's
`LumpedPort`/`WavePort` `Direction` field can be populated after rendering.

Rendering is unaffected: `to_polygons` and `to_primitives` pass through to the underlying entity.
The direction transforms with the entity under rotation or reflection via
`transform(sty::WithDirection, f::Transformation) = WithDirection(rotated_direction(sty.direction, f))`,
so after `plan!`/`build!`/`index_layer!` the carried angle describes the global
orientation.

If an angle is given without units, it is assumed to be in radians.
The stored angle is **not** automatically normalized to `[0°, 360°)`.
"""
struct WithDirection <: GeometryEntityStyle
    direction::typeof(1.0°)
    # Constrain the inner constructor to a Number so WithDirection(::GeometryEntity)
    # routes via the generic `(T::Type{<:GeometryEntityStyle})(x::GeometryEntity, args...)`
    # fallback instead of colliding with this constructor (silences Aqua).
    WithDirection(direction::Number) = new(uconvert(°, direction))
end
# Default constructor — no-arg form is unambiguous.
WithDirection() = WithDirection(0°)

to_polygons(ent::GeometryEntity, ::WithDirection; kwargs...) = to_polygons(ent; kwargs...)

transform(sty::WithDirection, f::Transformation) =
    WithDirection(rotated_direction(sty.direction, f))

# Walk through any nesting of StyledEntity wrappers and return the `direction`
# angle of the first `WithDirection` style encountered (from outside in), or `nothing` if no
# `WithDirection` is present. Handles nesting like
# WithDirection(MeshSized(only_simulated(rect))) and the reverse.
extract_direction(::DeviceLayout.GeometryEntity) = nothing
function extract_direction(ent::DeviceLayout.StyledEntity)
    return extract_direction(ent.ent)
end
function extract_direction(
    ent::DeviceLayout.StyledEntity{T, U, WithDirection}
) where {T, U <: GeometryEntity{T}}
    return ent.sty.direction
end
