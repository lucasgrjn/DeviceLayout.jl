module SolidModels

import Gmsh: gmsh, gmsh.model.occ
export gmsh, populate_size_fields!

# Explicit callback dictionary, to overcome closure limitation on apple silicon.
import StaticArrays: SVector
import NearestNeighbors: KDTree
import Distances: Euclidean
const MESHSIZE_PARAMS = Dict{
    Symbol,
    Union{
        Float64,
        Int64,
        Dict{Tuple{Float64, Float64}, Vector{SVector{3, Float64}}},
        Dict{
            Tuple{Float64, Float64},
            KDTree{SVector{3, Float64}, Euclidean, Float64, SVector{3, Float64}}
        }
    }
}(
    :mesh_scale => 1.0,
    :mesh_order => 1,
    :global_α => 0.75,
    :cp => Dict{Tuple{Float64, Float64}, Vector{SVector{3, Float64}}}(),
    :ct => Dict{
        Tuple{Float64, Float64},
        KDTree{SVector{3, Float64}, Euclidean, Float64, SVector{3, Float64}}
    }()
) # initial defaults

import DeviceLayout
import DeviceLayout:
    AbstractCoordinateSystem,
    AbstractPolygon,
    ClippedPolygon,
    Coordinate,
    CoordinateSystem,
    CurvilinearPolygon,
    CurvilinearRegion,
    GeometryEntity,
    GeometryEntityStyle,
    Ellipse,
    LineSegment,
    Meta,
    MeshSized,
    NoRender,
    OptionalStyle,
    Paths,
    Plain,
    Polygon,
    StyledEntity,
    Point,
    save,
    getx,
    gety,
    points,
    pathtopolys,
    perimeter,
    flatten,
    dimension,
    elements,
    element_metadata,
    _compound_pin_render,
    to_polygons,
    layer,
    level,
    norm,
    render!,
    uniquename,
    isapprox_angle
import DeviceLayout.Paths: trace, gap, offset, extent, pathlength, bspline_approximation
import DeviceLayout.Polygons:
    RelativeRounded,
    Rounded,
    orientation,
    StyleDict,
    cornerindices,
    iscircle,
    perimeter,
    center,
    angle,
    r1,
    r2,
    radius
import DeviceLayout.Curvilinear:
    islinear, round_to_curvilinearpolygon, to_curvilinear, styled_loop
import Unitful: μm, mm, ustrip, °, uconvert, Length, unit
import FileIO: File

import SpatialIndexing
import SpatialIndexing: RTree

# Units that all dimensions will be converted to before rendering to STP
const STP_UNIT = μm

abstract type AbstractPhysicalGroup end
const DimGroupDict = Dict{String, AbstractPhysicalGroup}

Base.broadcastable(p::AbstractPhysicalGroup) = Ref(p)

"""
    abstract type SolidModelKernel

Supertype for solid geometry kernels. Subtypes are `OpenCascade` and `GmshNative`.

Note that `GmshNative` does not support Boolean geometry operations.
"""
abstract type SolidModelKernel end
struct OpenCascade <: SolidModelKernel end
struct GmshNative <: SolidModelKernel end

Base.broadcastable(x::SolidModelKernel) = Ref(x)

kernelmodule(::OpenCascade) = gmsh.model.occ
kernelmodule(::GmshNative) = gmsh.model.geo

function Base.getproperty(k::SolidModelKernel, s::Symbol)
    return getproperty(kernelmodule(k), s)
end

function Base.propertynames(k::SolidModelKernel)
    return propertynames(kernelmodule(k))
end

"""
    struct SolidModel{T} where {T <: SolidModelKernel}
        name::String
        groups::NTuple{4,Dict{String,AbstractPhysicalGroup}}
        kernel::T
    end
    SolidModel(name::String, kernel::SolidModelKernel=OpenCascade(); overwrite=false)

A 3D geometry model.

Geometry rendering, boolean operations, and export are provided by the specified `kernel`.

Physical groups can be accessed by name using indexing with a `String` or `Symbol` and a
dimension: `mymodel["mygroup", 3]` will return the `PhysicalGroup` with dimension 3 called
`mygroup`.

Physical groups can also be assigned as `mymodel["mygroup"] = dimtags`, where `dimtags`
is a list of `NTuple{2, Int32}` of entities identified by `(dim, tag)` in `mymodel`.
If `dimtags` includes entities of multiple dimensions, then a group is created for each
dimension.

If the constructor is called with `overwrite=false` (the default), then an error will be thrown if a
model with the same name already exists. If `overwrite=true`, then any existing model
with the same name will be deleted and a new model will be created.

A `SolidModel` can be saved to a file with `FileIO.save(filename, sm)`.
Supported filetypes for OpenCASCADE geometries are `.brep` and `.stp`.
Meshes can be exported as `.msh2` (compatible with Palace) or `.msh` (most recent Gmsh format) files.
Other filetypes supported by `gmsh.write()` like `.xao` can be used with `DeviceLayout.save`.
"""
struct SolidModel{T <: SolidModelKernel}
    name::String
    groups::NTuple{4, DimGroupDict}
    kernel::T

    function SolidModel(
        name::String,
        kernel::SolidModelKernel=OpenCascade();
        overwrite=false
    )
        iszero(gmsh.is_initialized()) && gmsh.initialize()
        # SolidModel initiated gmsh uses μm
        set_gmsh_option("Geometry.OCCTargetUnit", "UM")
        # Use threads in open cascade
        set_gmsh_option("Geometry.OCCParallel", 1)
        # Default to no threads, as there appear to be race conditions within gmsh.
        # If set to zero, Gmsh will look for OMP_NUM_THREADS environment variables;
        # this needs to be >1 for HXT algorithm to use parallelism.
        set_gmsh_option("General.NumThreads", 1)

        # Reasonable defaults for meshing.
        set_gmsh_option("Mesh.MeshSizeFromPoints", 0)
        set_gmsh_option("Mesh.MeshSizeFromCurvature", 0)
        set_gmsh_option("Mesh.MeshSizeExtendFromBoundary", 0)
        set_gmsh_option("Mesh.Algorithm", 6)
        set_gmsh_option("Mesh.Algorithm3D", 1)

        # Always save meshes in binary for faster disk I/O
        set_gmsh_option("Mesh.Binary", 1)

        # If a model with this name exists, throw error or delete it
        names = gmsh.model.list()
        if name in names
            !overwrite && error(
                "Gmsh model $name already exists; use SolidModel($name; overwrite=true) to overwrite"
            )
            gmsh.model.set_current(name) # Activate the old model "name"
            gmsh.model.remove() # Remove the old model
        end
        gmsh.model.add(name) # Add and set as current model

        return new{typeof(kernel)}(
            name,
            (DimGroupDict(), DimGroupDict(), DimGroupDict(), DimGroupDict()),
            kernel
        )
    end
end
Base.broadcastable(x::SolidModel) = Ref(x)

summary(sm::SolidModel) = string(
    "SolidModel ",
    repr(sm.name),
    " (",
    nameof(typeof(sm.kernel)),
    " kernel) with ",
    sum(length, sm.groups),
    " physical group",
    sum(length, sm.groups) == 1 ? "" : "s"
)
Base.show(io::IO, sm::SolidModel) = print(io, summary(sm))

function _physical_group_entity_counts(sm::SolidModel)
    gmsh.is_initialized() == 0 && return nothing
    models = gmsh.model.list()
    name(sm) in models || return nothing
    current = gmsh.model.get_current()
    gmsh.model.set_current(name(sm))
    try
        counts = Dict{Tuple{Int, String}, Int}()
        for (index, groups) in enumerate(sm.groups)
            for (groupname, group) in groups
                counts[(index - 1, groupname)] = length(entitytags(group))
            end
        end
        return counts
    finally
        current in gmsh.model.list() && gmsh.model.set_current(current)
    end
end

function Base.show(io::IO, ::MIME"text/plain", sm::SolidModel)
    print(io, summary(sm))
    counts = _physical_group_entity_counts(sm)
    for dim = 3:-1:0
        groups = sm.groups[dim + 1]
        isempty(groups) && continue
        names = sort!(collect(keys(groups)))
        print(io, "\n  dim ", dim, ":")
        maxitems = get(io, :limit, false)::Bool ? 10 : length(names)
        shown =
            length(names) <= maxitems ? eachindex(names) :
            Iterators.flatten((
                1:(maxitems ÷ 2),
                (length(names) - maxitems ÷ 2 + 1):length(names)
            ))
        lastidx = 0
        for i in shown
            i > lastidx + 1 && print(io, "\n   ⋮")
            groupname = names[i]
            print(io, "\n   ")
            show(io, groupname)
            if isnothing(counts)
                print(io, ": entity count unavailable")
            else
                count = counts[(dim, groupname)]
                print(io, ": ", count, count == 1 ? " entity" : " entities")
            end
            lastidx = i
        end
    end
    return nothing
end

"""
    struct PhysicalGroup
        name::String
        model::SolidModel
        dim::Int32
        grouptag::Int32
    end

A named group of entities of dimension `dim` in a `SolidModel`.
"""
struct PhysicalGroup <: AbstractPhysicalGroup
    name::String
    model::SolidModel
    dim::Int32
    grouptag::Int32
end

summary(pg::PhysicalGroup) =
    "Physical Group $(pg.name) of dimension $(pg.dim) with $(length(dimtags(pg))) entities"
Base.show(io::IO, pg::PhysicalGroup) = print(io, summary(pg))

"""
    entitytags(pg::AbstractPhysicalGroup)

Return the integer tags for `SolidModel` entities in `pg`.
"""
function entitytags(pg::AbstractPhysicalGroup)
    if (pg.dim, pg.grouptag) in gmsh.model.getPhysicalGroups(pg.dim)
        return gmsh.model.getEntitiesForPhysicalGroup(pg.dim, pg.grouptag)
    end
    return Int32[]
end

"""
    dimtags(pg::AbstractPhysicalGroup)

Return the `(dimension, integer tag)` tuples for `SolidModel` entities in `pg`.
"""
dimtags(pg::AbstractPhysicalGroup) = [(pg.dim, tag) for tag in entitytags(pg)]
dimtags(groups::Vector{<:AbstractPhysicalGroup}) =
    reduce(vcat, dimtags.(groups); init=Tuple{Int32, Int32}[])

name(pg::PhysicalGroup) = pg.name

name(sm::SolidModel) = sm.name
kernel(sm::SolidModel) = sm.kernel
kernel(pg::AbstractPhysicalGroup) = kernel(pg.model)
function kernel(pg::AbstractArray{<:AbstractPhysicalGroup})
    isempty(pg) && error("Cannot establish kernel for empty array.")
    return kernel(pg[1].model)
end
model(pg::PhysicalGroup) = pg.model
function model(pg::AbstractArray{<:AbstractPhysicalGroup})
    isempty(pg) && error("Cannot establish model for empty array.")
    return model(pg[1])
end

_synchronize!(sm::SolidModel) = kernel(sm).synchronize()

### SolidModel API
"""
    dimgroupdict(sm::SolidModel, dim::Int)

The `PhysicalGroup`s of dimension `dim` in `sm`, as a `DimGroupDict` (alias for `Dict{String, AbstractPhysicalGroup}`).
"""
dimgroupdict(sm::SolidModel, dim::Integer) = sm.groups[dim + 1]

"""
    getindex(sm::SolidModel, name::String, dim::Int)

Get the `PhysicalGroup` with name `name` and dimension `dim`.
"""
function Base.getindex(sm::SolidModel, name::String, dim::Integer)::PhysicalGroup
    return dimgroupdict(sm, dim)[name]
end
Base.getindex(sm::SolidModel, name::Symbol, dim) = getindex(sm, string(name), dim)

"""
    hasgroup(sm::SolidModel, name::String, dim::Integer)

Return `true` if `sm` has a group of dimension `dim` named `name`, and return `false` otherwise.
"""
function hasgroup(sm::SolidModel, name::String, dim::Integer)
    return haskey(dimgroupdict(sm, dim), name)
end
hasgroup(sm::SolidModel, name::Symbol, dim::Integer) = hasgroup(sm, string(name), dim)

"""
    setindex!(sm::SolidModel, dimtags, name::String)

Create a `PhysicalGroup` with name `name` and `(dim, tag)` integer pairs `dimtags`.

If `dimtags` contains elements of different dimensions, a group is created for each
dimension.

If any groups with the same name and dimension already exists, they will be removed and replaced
with the new groups. (No entities are deleted, just the group entries.)
"""
function Base.setindex!(sm::SolidModel, dimtags, groupname::String)
    gmsh.model.set_current(name(sm))
    _synchronize!(sm) # Need to synchronize to add elements to physical group
    # Set the group for each dimension
    for dim in unique(first.(dimtags))
        tags = last.(dimtags[first.(dimtags) .== dim])
        if hasgroup(sm, groupname, dim)
            pg = sm[groupname, dim]
            gmsh.model.remove_physical_groups([(dim, pg.grouptag)])
        end
        tag = gmsh.model.addPhysicalGroup(dim, tags, -1, groupname)
        dimgroupdict(sm, dim)[groupname] = PhysicalGroup(groupname, sm, dim, tag)
        gmsh.model.setPhysicalName(dim, tag, groupname)
    end
end
Base.setindex!(sm::SolidModel, dimtags, name::Symbol) = setindex!(sm, dimtags, string(name))

"""
    get(sm::SolidModel, name, dim, default::AbstractPhysicalGroup)

Get the `PhysicalGroup` with name `name` and dimension `dim` or return `default`.
"""
function Base.get(sm::SolidModel, name, dim, default::AbstractPhysicalGroup)
    return hasgroup(sm, name, dim) ? sm[name, dim] : default
end

"""
    save(file::File, sm::SolidModel)
    save(filename::String, sm::SolidModel)

Save a `SolidModel` instance to a `file` or `filename`.

Using `FileIO.save`, supported filetypes using for OpenCASCADE geometries are `.brep` and `.stp`.
Meshes can be exported as `.msh2` (compatible with Palace) or `.msh` (most recent Gmsh format) files.

Using `DeviceLayout.save`, you can also choose any other extension supported by `gmsh.write()` like `.xao`.
"""
function save(file::File, sm::SolidModel)
    gmsh.model.set_current(name(sm))
    _synchronize!(sm)
    return gmsh.write(file.filename)
end
function save(filename::String, sm::SolidModel)
    gmsh.model.set_current(name(sm))
    _synchronize!(sm)
    return gmsh.write(filename)
end

"""
    bounds3d(group::AbstractPhysicalGroup; delta=0)
    bounds3d(dimtags; delta=0)

Return the rectangular prism defined bounding `group` or `dimtags` with an offset of `delta`.

Note that OpenCASCADE bounding boxes are not tight, and will typically extend beyond the exact
bounding box by 1e-7μm at each face.

The result is returned as the tuple `(xmin, ymin, zmin, xmax, ymax, zmax)`.
"""
function bounds3d(group::AbstractPhysicalGroup; delta=0)
    return bounds3d(dimtags(group), delta=delta)
end

function bounds3d(dims_tags; delta=0)
    xmin, ymin, zmin = Inf, Inf, Inf
    xmax, ymax, zmax = -Inf, -Inf, -Inf
    for (dim, tag) in dims_tags
        x1, y1, z1, x2, y2, z2 = gmsh.model.getBoundingBox(dim, tag)
        xmin = min(xmin, x1)
        ymin = min(ymin, y1)
        zmin = min(zmin, z1)
        xmax = max(xmax, x2)
        ymax = max(ymax, y2)
        zmax = max(zmax, z2)
    end
    return xmin - delta,
    ymin - delta,
    zmin - delta,
    xmax + delta,
    ymax + delta,
    zmax + delta
end

"""
    reindex_physical_groups!(sm::SolidModel)

Reassign all physical group tags so that the numbering is 1-based and contiguous. This is
not necessary for the mesh to be valid, but helps with human readability in generated config files.
"""
function reindex_physical_groups!(sm::SolidModel)
    # Create a copy of all entity tags in each physical group, then delete them.
    dim_name_to_tag = Vector{Tuple{Int, String, Vector{Int}}}()
    for dim = 3:-1:0
        for (n, _) ∈ dimgroupdict(sm, dim)
            push!(dim_name_to_tag, (dim, n, entitytags(sm[n, dim])))
            gmsh.model.remove_physical_groups([(dim, dimgroupdict(sm, dim)[n].grouptag)])
        end
    end

    # Sort with descending integer, then in alphabetical
    function comparator(x, y)
        if x[1] > y[1]
            return true
        elseif x[1] < y[1]
            return false
        else
            return x[2] < y[2]
        end
    end
    sort!(dim_name_to_tag, lt=comparator)

    # Re-add physical groups
    grouptag = 1
    for (dim, n, tags) ∈ dim_name_to_tag
        newtag = gmsh.model.add_physical_group(dim, tags, grouptag, n)
        @assert newtag == grouptag
        dimgroupdict(sm, dim)[n] = PhysicalGroup(n, sm, dim, grouptag)
        grouptag = grouptag + 1
    end
end

"""
    attributes(sm::SolidModel)

Given a `SolidModel` construct a dictionary from physical group name to attribute number for
use in specification of a configuration file for use with *Palace*.
"""
function attributes(sm::SolidModel)
    attributes = Dict{String, Int}()
    for d = 0:3
        for (k, v) ∈ dimgroupdict(sm, d)
            attributes[k] = v.grouptag
        end
    end
    return attributes
end

include("render.jl")
include("postrender.jl")
include("conformal/conformal.jl")

using .ConformalRender: render_conformal!, ConformalRenderContext, add_conformal_loop!
export render_conformal!, ConformalRenderContext, add_conformal_loop!

end # module
