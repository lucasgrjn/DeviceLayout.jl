######## Rendering
import Clipper: children, contour, ishole, PolyNode
import Unitful: Length
import StaticArrays: SVector
import NearestNeighbors: nn

"""
    to_primitives(::SolidModel, ent::GeometryEntity; kwargs...)

Return a `GeometryEntity` or a vector of entities equivalent to `ent`.

Called inside `render!` before adding entities to the `SolidModel`. Each resulting entity
corresponds to a single entity in that `SolidModel`'s geometry kernel.

If there is no special handling for `ent` in the kernel, then the result will be
`to_polygons(ent; kwargs...)`.
"""
to_primitives(::SolidModel, ent::GeometryEntity; kwargs...) = to_polygons(ent; kwargs...)

# Use the same linearity gate as the GDS path renderer so both paths choose plain polygons vs
# symbolic curvilinear geometry from one source of truth.
function to_primitives(sm::SolidModel, node::Paths.Node; kwargs...)
    return to_primitives(sm, node, islinear(node.seg, node.sty); kwargs...)
end
# GmshNative can't ingest native arcs/splines, so flatten all path nodes here.
# Keeping this in to_primitives makes mesh-size sampling use the same polygons
# that are added to the kernel.
to_primitives(::SolidModel{GmshNative}, node::Paths.Node; kwargs...) =
    to_polygons(node; kwargs...)
# Path nodes that can be drawn with only polygons.
function to_primitives(::SolidModel, node::Paths.Node, ::Val{true}; kwargs...)
    return to_polygons(node; kwargs...)
end

to_primitives(sm::SolidModel, ent::StyledEntity{T, U, S}; kwargs...) where {T, U, S} =
    to_primitives(sm, ent.ent; kwargs...)
function to_primitives(
    sm::SolidModel{OpenCascade},
    ent::StyledEntity{T, U, S};
    kwargs...
) where {T, U, S <: Rounded}
    inner_prim = to_primitives(sm, ent.ent; kwargs...)
    if inner_prim isa Vector
        return to_primitives(sm, ent.sty.(inner_prim); kwargs...)
    end
    return to_primitives(sm, ent.sty(inner_prim); kwargs...)
end

function to_primitives(sm::SolidModel, ent::Vector{<:GeometryEntity}; kwargs...)
    return vcat(to_primitives.(sm, ent; kwargs...)...)
end

# Flatten the ClippedPolygon to a collection of non-overlapping CurvilinearRegion
function to_primitives(::SolidModel, ent::ClippedPolygon{T}; kwargs...) where {T}
    # Flatten the tree into a collection of CurvilinearRegion
    flat = CurvilinearRegion{T}[]
    function add_region(node)
        push!(flat, CurvilinearRegion(contour(node), contour.(node.children)))
        for n ∈ node.children
            add_region.(n.children) # Add all grand children -- positives
        end
    end
    add_region.(ent.tree.children)
    return flat
end

function to_primitives(
    ::SolidModel,
    ent::StyledEntity{T, ClippedPolygon{T}, <:StyleDict};
    kwargs...
) where {T}
    return to_curvilinear(ent.ent, ent.sty; kwargs...)
end

# GmshNative flattens clipped regions up front for the same reason as path nodes.
to_primitives(::SolidModel{GmshNative}, ent::ClippedPolygon; kwargs...) =
    to_polygons(ent; kwargs...)
to_primitives(
    ::SolidModel{GmshNative},
    ent::StyledEntity{T, ClippedPolygon{T}, <:StyleDict};
    kwargs...
) where {T} = to_polygons(ent.ent, ent.sty; kwargs...)

function to_primitives(::SolidModel, ent::Ellipse; rounded=nothing, Δθ=nothing, kwargs...)
    if !isnothing(rounded)
        Base.depwarn(
            "The `rounded` keyword for Ellipse is deprecated. Use `Δθ=nothing` (default) to keep as ellipse primitive, or `Δθ=some_angle` to discretize to polygon. For the same discretization as `rounded=false` with `Δθ` not specified, use `Δθ=360°/8`",
            :to_primitives
        )
        rounded && return ent  # Keep as ellipse primitive
        # Otherwise, use the old default Δθ for backward compatibility
        return to_polygons(ent; Δθ=(isnothing(Δθ) ? 360° / 8 : Δθ), kwargs...)
    else
        isnothing(Δθ) && return ent  # Keep as ellipse primitive (default code path)
        # Otherwise, use specified Δθ
        return to_polygons(ent; Δθ, kwargs...)  # Discretize to polygon
    end
end
function to_primitives(
    ::SolidModel{GmshNative},
    ent::Ellipse;
    rounded=nothing,
    Δθ=nothing,
    kwargs...
)
    if !isnothing(rounded)
        Base.depwarn(
            "The `rounded` keyword for Ellipse is deprecated. Use `Δθ=some_angle` to discretize to a polygon; for the same discretization as `rounded=false` with `Δθ` not specified, use `Δθ=360°/8`",
            :to_primitives
        )
        if !rounded && isnothing(Δθ)
            return to_polygons(ent; Δθ=360° / 8, kwargs...)
        end
    end
    return to_polygons(ent; Δθ, kwargs...)
end

# Path nodes that can be drawn with native curves (in OCC)
# Gmsh does have its own native curves but we don't use them (the APIs and particularly
# spline representations are different)
function to_primitives(sm::SolidModel, node::Paths.Node, ::Val{false}; kwargs...)
    return to_primitives(sm, node.seg, node.sty; kwargs...)
end

# CurvilinearRegion is a primitive
to_primitives(::SolidModel, ent::CurvilinearPolygon; kwargs...) = CurvilinearRegion(ent)
to_primitives(::SolidModel, ent::CurvilinearRegion; kwargs...) = ent
# GmshNative flattens curvilinear primitives before they reach the kernel.
to_primitives(::SolidModel{GmshNative}, ent::CurvilinearPolygon; kwargs...) =
    to_polygons(ent; kwargs...)
to_primitives(::SolidModel{GmshNative}, ent::CurvilinearRegion; kwargs...) =
    to_polygons(ent; kwargs...)

# LineSegment is a primitive
to_primitives(::SolidModel, ent::LineSegment; kwards...) = ent

######## Optional Render
function to_primitives(
    sm::SolidModel,
    e::StyledEntity{T, U, NoRender};
    kwargs...
) where {T, U}
    return Polygon{T}[]
end

function to_primitives(
    sm::SolidModel,
    ent::StyledEntity{T, U, OptionalStyle};
    kwargs...
) where {T, U <: GeometryEntity}
    sty =
        get(kwargs, ent.sty.flag, ent.sty.default) ? ent.sty.true_style :
        ent.sty.false_style
    return to_primitives(sm, StyledEntity(ent.ent, sty); kwargs...)
end

######## Rounded polygons
# Rounded polygons expand to their exact-arc CurvilinearPolygon through the shared converter.
function to_primitives(
    ::SolidModel{OpenCascade},
    ent::StyledEntity{T, Polygon{T}, <:Rounded};
    kwargs...
) where {T}
    return CurvilinearRegion(to_curvilinear(ent.ent, ent.sty; kwargs...))
end

# Convert a single style to a style dict
function to_primitives(
    sm::SolidModel{OpenCascade},
    ent::StyledEntity{T, CurvilinearRegion{T}};
    kwargs...
) where {T}
    return to_primitives(sm, StyleDict(ent.sty)(ent.ent); kwargs...)
end

function to_primitives(
    ::SolidModel{OpenCascade},
    ent::StyledEntity{T, CurvilinearRegion{T}, <:StyleDict};
    kwargs...
) where {T}
    return to_curvilinear(ent.ent, ent.sty; kwargs...)
end

######## Ellipse
function _add_to_current_solidmodel!(
    e::Ellipse{T},
    m::Meta,
    k;
    zmap=(_) -> zero(T),
    kwargs...
) where {T}
    z = zmap(m) # map from m using kwargs

    c = ustrip(STP_UNIT, center(e))
    line = k.add_ellipse(
        c[1],
        c[2],
        ustrip(STP_UNIT, z),
        ustrip(STP_UNIT, r1(e)),
        ustrip(STP_UNIT, r2(e)),
        -1,
        0.0,
        2 * π,
        [0.0, 0.0, 1.0],
        [cos(angle(e)), sin(angle(e)), 0.0]
    )
    loop = k.add_curve_loop([line])
    surf = k.add_plane_surface([loop])

    return (Int32(2), surf)
end

######## Path nodes to primitives
# Note: this is called during SolidModel rendering after flattening, so we don't worry about decorations
# Similarly generic tapers have been resolved

# Fallback: use pathtopolys to get CurvilinearPolygons
function to_primitives(
    ::SolidModel,
    f::Paths.Segment{T},
    s::Paths.Style;
    kwargs...
) where {T}
    iszero(Paths.pathlength(f)) &&
        return Union{CurvilinearPolygon{T}, CurvilinearRegion{T}}[]
    return pathtopolys(f, s; kwargs...)
end

function to_primitives(
    sm::SolidModel,
    f::Paths.CompoundSegment{T},
    s::Paths.CompoundStyle;
    kwargs...
) where {T}
    return vcat(to_primitives.(sm, f.segments, s.styles; kwargs...)...)
end

# Compound segment + single style: shared loop with the
# SolidModel leaf `to_primitives(sm, …)`, which adds the zero-length guard the GDS leaf lacks.
function to_primitives(
    sm::SolidModel,
    f::Paths.CompoundSegment{T},
    s::Paths.Style;
    kwargs...
) where {T}
    return _compound_pin_render(f, s, (se, sty) -> to_primitives(sm, se, sty; kwargs...))
end

function to_primitives(
    sm::SolidModel,
    f::Paths.Segment{T},
    s::Paths.PeriodicStyle;
    kwargs...
) where {T}
    subsegs, substys = Paths.resolve_periodic(f, s)
    return vcat(to_primitives.(sm, subsegs, substys; kwargs...)...)
end
# Disambiguate
function to_primitives(
    sm::SolidModel,
    f::Paths.CompoundSegment{T},
    s::Paths.PeriodicStyle;
    kwargs...
) where {T}
    subsegs, substys = Paths.resolve_periodic(f, s)
    return vcat(to_primitives.(sm, subsegs, substys; kwargs...)...)
end

# Terminations generate up to two [Rounded] Polygons
function to_primitives(
    sm::SolidModel,
    seg::Paths.Segment{T},
    sty::Union{Paths.TraceTermination, Paths.CPWOpenTermination, Paths.CPWShortTermination};
    kwargs...
) where {T}
    return to_primitives(sm, DeviceLayout._poly(seg, sty); kwargs...)
end
# Disambiguate
function to_primitives(
    sm::SolidModel,
    seg::Paths.CompoundSegment{T},
    sty::Union{Paths.TraceTermination, Paths.CPWOpenTermination, Paths.CPWShortTermination};
    kwargs...
) where {T}
    return to_primitives(sm, DeviceLayout._poly(seg, sty); kwargs...)
end

meshsize(::GeometryEntity{T}; kwargs...) where {T} = float(ustrip(STP_UNIT, zero(T)))
meshsize(::GeometryEntity{<:Real}; kwargs...) = 0.0
meshsize(ent::StyledEntity{T, StyledEntity{T, R, U}, S}; kwargs...) where {T, R, U, S} =
    meshsize(ent.ent; kwargs...)
# Always choose outermost MeshSized
meshsize(
    ent::StyledEntity{T, StyledEntity{T, R, U}, S};
    kwargs...
) where {T, R, U, S <: MeshSized} = meshsize(ent.sty; kwargs...)
meshsize(ent::StyledEntity; kwargs...) = meshsize(ent.sty; kwargs...)
meshsize(s::GeometryEntityStyle; kwargs...) = 0.0
meshsize(s::MeshSized{T}; kwargs...) where {T} = ustrip(STP_UNIT, s.h)
meshsize(s::OptionalStyle; kwargs...) =
    get(kwargs, s.flag, s.default) ? meshsize(s.true_style; kwargs...) :
    meshsize(s.false_style; kwargs...)
# Path node mesh depends only on style
meshsize(node::Paths.Node{T}; kwargs...) where {T} =
    ustrip(STP_UNIT, meshsize(node.seg, node.sty; kwargs...))
meshsize(node::Paths.Node{<:Real}; kwargs...) = meshsize(node.seg, node.sty; kwargs...)
# Various path styles
meshsize(seg::Paths.Segment, ::Paths.Style; kwargs...) = zero(eltype(seg))
meshsize(::Paths.Segment, sty::Paths.SimpleTrace; kwargs...) = 2 * sty.width
meshsize(::Paths.Segment, sty::Paths.SimpleCPW; kwargs...) = 2 * min(sty.trace, sty.gap)
meshsize(::Paths.Segment, sty::Paths.TaperTrace; kwargs...) =
    2 * min(sty.width_start, sty.width_end)
meshsize(::Paths.Segment, sty::Paths.TaperCPW; kwargs...) =
    2 * min(sty.trace_start, sty.trace_end, sty.gap_start, sty.gap_end)
meshsize(::Paths.Segment, sty::Paths.TraceTermination; kwargs...) = 2 * sty.width
meshsize(::Paths.Segment, sty::Paths.CPWOpenTermination; kwargs...) =
    2 * min(sty.trace, sty.gap)
meshsize(::Paths.Segment, sty::Paths.CPWShortTermination; kwargs...) =
    2 * min(sty.trace, sty.gap)

# For GeneralCPW and GeneralTrace, just sample.
function meshsize(seg::Paths.Segment, sty::Paths.GeneralTrace; kwargs...)
    l = pathlength(seg)
    return 2 * minimum(sty.width.(range(zero(l), l, length=11)))
end
function meshsize(seg::Paths.Segment, sty::Paths.GeneralCPW; kwargs...)
    l = pathlength(seg)
    mintrace = minimum(sty.trace.(range(zero(l), l, length=11)))
    mingap = minimum(sty.gap.(range(zero(l), l, length=11)))
    return 2 * min(mintrace, mingap)
end
# Compound
function meshsize(seg::Paths.CompoundSegment, sty::Paths.CompoundStyle; kwargs...)
    return minimum(meshsize.(seg.segments, sty.styles))
end
# There should be no DecoratedStyles at SolidModel rendering
meshgrading(::GeometryEntity{T}; kwargs...) where {T} = -1.0
meshgrading(ent::StyledEntity{T, StyledEntity{T, R, U}, S}; kwargs...) where {T, R, U, S} =
    meshgrading(ent.ent; kwargs...)
# Always choose outermost MeshSized
meshgrading(
    ent::StyledEntity{T, StyledEntity{T, R, U}, S};
    kwargs...
) where {T, R, U, S <: MeshSized} = meshgrading(ent.sty; kwargs...)
meshgrading(ent::StyledEntity; kwargs...) = meshgrading(ent.sty; kwargs...)
meshgrading(::GeometryEntityStyle; kwargs...) = -1.0
meshgrading(s::OptionalStyle; kwargs...) =
    get(kwargs, s.flag, s.default) ? meshgrading(s.true_style; kwargs...) :
    meshgrading(s.false_style; kwargs...)
meshgrading(s::MeshSized{T}; kwargs...) where {T} = s.α
# All paths default to background grading
meshgrading(node::Paths.Node; kwargs...) = -1.0

# Used to define keys for grouping mesh size fields.
sizeandgrading(e::GeometryEntity; kwargs...) =
    (float(meshsize(e; kwargs...)), float(meshgrading(e; kwargs...)))

"""
    set_gmsh_option(s, o::Number)
    set_gmsh_option(s, o::AbstractString)
    set_gmsh_option(s, d::Dict, default)
    set_gmsh_option(d::Dict)

Set gmsh configuration options.

# Methods

  - `set_gmsh_option(option_name, value)`: Set a single option to a numeric or string value
  - `set_gmsh_option(option_name, dict, default)`: Set option from dict with fallback to default
  - `set_gmsh_option(option_name, dict)`: Set option from dict if `option_name` is present
  - `set_gmsh_option(dict)`: Set multiple options from a dictionary

# Arguments

  - `s`: Option name as string (e.g., "Mesh.Algorithm", "General.NumThreads")
  - `o`: Option value (Number or String)
  - `d`: Dictionary containing option name-value pairs
  - `default`: Default value if option not found in dictionary

# Examples

```julia
set_gmsh_option("Mesh.Algorithm", 6)
set_gmsh_option("General.FileName", "output.msh")
set_gmsh_option("General.FileName", Dict("Mesh.Algorithm" => 6)) # does nothing
set_gmsh_option(Dict("Mesh.Algorithm" => 6, "General.NumThreads" => 4))
```
"""
set_gmsh_option(s, o::Number) = SolidModels.gmsh.option.set_number(s, o)
set_gmsh_option(s, o::AbstractString) = SolidModels.gmsh.option.set_string(s, o)
function set_gmsh_option(s, d::Dict, default)
    return set_gmsh_option(s, get(d, s, default))
end
function set_gmsh_option(s, d::Dict)
    return haskey(d, s) && set_gmsh_option(s, d[s])
end
function set_gmsh_option(d::Dict)
    for (k, v) in d
        set_gmsh_option(k, v)
    end
end

"""
    get_gmsh_number(s)

Get a numeric option value from gmsh.

# Arguments

  - `s`: Option name as string (e.g., "Mesh.ElementOrder")

Returns the current numeric value of the specified gmsh option.
"""
get_gmsh_number(s) = gmsh.option.get_number(s)

"""
    get_gmsh_string(s)

Get a string option value from gmsh.

# Arguments

  - `s`: Option name as string (e.g., "General.FileName")

Returns the current string value of the specified gmsh option.
"""
get_gmsh_string(s) = gmsh.option.get_string(s)

"""
    mesh_scale()
    mesh_scale(s)

Get or set the global mesh scaling factor.

The mesh scale adjusts the minimum for all size fields, from `h` adjacent to a sized entity,
to `mesh_scale * h`. It does not reduce the size in the far field, and is most appropriate
for refining geometric features such as curves, which might require additional local
refinement to capture the geometry, but do not require refinement non-locally.

See [`DeviceLayout.MeshSized`](@ref) for more details and the explicit mesh sizing formula.
"""
mesh_scale(s) = MESHSIZE_PARAMS[:mesh_scale]::Float64 = s
mesh_scale() = MESHSIZE_PARAMS[:mesh_scale]::Float64

"""
    mesh_order()
    mesh_order(order, higher_order_optimize=1)

Get or set the mesh element order and optimization level.

Higher order elements provide better geometric fidelity for curved boundaries but increase meshing complexity.
"""
mesh_order() = SolidModels.gmsh.option.get_number("Mesh.ElementOrder")
function mesh_order(order::Number, higher_order_optimize::Number=1)
    set_gmsh_option("Mesh.ElementOrder", order)
    set_gmsh_option("Mesh.HighOrderOptimize", higher_order_optimize)
    return nothing
end

"""
    mesh_grading_default()
    mesh_grading_default(α)

Get or set the default mesh grading parameter.

Controls how rapidly mesh size changes with distance from control points. Must satisfy 0 < α
≤ 1.

See [`DeviceLayout.MeshSized`](@ref) for more details and the explicit mesh sizing formula.
"""
mesh_grading_default() = MESHSIZE_PARAMS[:global_α]::Float64
function mesh_grading_default(α)
    @assert 0 < α <= 1
    MESHSIZE_PARAMS[:global_α]::Float64 = α
    finalize_size_fields!()
    return MESHSIZE_PARAMS[:global_α]
end

"""
    add_mesh_size_point(; h, α=-1, p)

Add a mesh size control point to the global mesh sizing parameters.

# Arguments

  - `p`: 3D point coordinates where mesh size is controlled. Can be a single point, or array
    of concatenated points [x1,y1,z1,x2,y2,z2,...].
  - `h`: Target mesh size at the point
  - `α`: Mesh grading parameter (α ≤ 1). If negative the default global value will be used
    when the size trees are regenerated. All negative values are mapped together for efficient
    KDTree calculations.

The point is added to a collection grouped by `(h, α)` values for efficient mesh size field
computation. This is a *manual override* that occurs in addition to those control points
generated by a geometry, in general mesh size points should be encoded directly within
component definitions but manual additional points can be helpful in prototyping.

See [`DeviceLayout.MeshSized`](@ref) for details and the explicit mesh sizing formula.
"""
function add_mesh_size_point(p; h, α=-1)
    return append!(
        get!(MESHSIZE_PARAMS[:cp], (h, α < 0 ? -1 : α), Vector{SVector{3, Float64}}()),
        reinterpret(SVector{3, Float64}, p)
    )
end

"""
    finalize_size_fields!()

Rebuild KDTree data structures for mesh size field computation.

Must be called after manually adding mesh size points with [`add_mesh_size_point`](@ref)
to enable efficient spatial queries during meshing. Creates KDTrees grouped by `(h, α)`
parameters for fast nearest-neighbor lookups.

See [`DeviceLayout.MeshSized`](@ref) for details and the explicit mesh sizing formula.
"""
function finalize_size_fields!()
    # For each collection of (h, α), can assemble a KDTree to find closest. This will be the
    # smallest mesh size over that collection of vertices, as size is proportional to
    # distance for this subset. Thereby the comparison over lengths need only be over the
    # number of different (h, α) combinations. This is most impactful for large graphs with
    # many duplicates of a given component, where there will be many points per (h, α).
    MESHSIZE_PARAMS[:ct] = Dict{
        Tuple{Float64, Float64},
        KDTree{SVector{3, Float64}, Euclidean, Float64, SVector{3, Float64}}
    }()
    normalized_points = Dict{Tuple{Float64, Float64}, Vector{SVector{3, Float64}}}()
    for ((h, α), points) in MESHSIZE_PARAMS[:cp]
        # Substitute any negative grading value for the global default. Delaying this
        # substitution allows for modifying the size field after rendering, without needing
        # to recompute the locations of all control points.
        key = (h, α < 0 ? MESHSIZE_PARAMS[:global_α] : α)
        append!(get!(normalized_points, key, SVector{3, Float64}[]), points)
    end
    for (key, points) in normalized_points
        MESHSIZE_PARAMS[:ct][key] = KDTree(points)
    end
    return nothing
end

"""
    mesh_control_points()

Get the dictionary of mesh size control points grouped by `(h, α)` parameters.

Returns a `Dict{Tuple{Float64, Float64}, Vector{SVector{3, Float64}}}` where keys are
`(mesh_size, grading_parameter)` tuples and values are vectors of 3D points.

If this dictionary is modified, by erasing points or adding points using
[`add_mesh_size_point`](@ref), then it is necessary to call [`finalize_size_fields!`](@ref)
to rebuild the KDTree from the data, else any resulting mesh will not reflect the change in
data.

See [`DeviceLayout.MeshSized`](@ref) for details and the explicit mesh sizing formula
utilizing the control points.
"""
mesh_control_points() =
    MESHSIZE_PARAMS[:cp]::Dict{Tuple{Float64, Float64}, Vector{SVector{3, Float64}}}

"""
    mesh_control_trees()

Get the dictionary of KDTrees for efficient spatial queries of mesh size control points.

Returns a `Dict{Tuple{Float64, Float64}, KDTree}` where keys are `(mesh_size, grading_parameter)`
tuples and values are KDTrees for fast nearest-neighbor lookups.

See [`DeviceLayout.MeshSized`](@ref) for details and the explicit mesh sizing formula
computed using the control trees.
"""
mesh_control_trees() = MESHSIZE_PARAMS[:ct]::Dict{
    Tuple{Float64, Float64},
    KDTree{SVector{3, Float64}, Euclidean, Float64, SVector{3, Float64}}
}

"""
    clear_mesh_control_points!()

Clear all mesh size control points and associated KDTrees.

See [`DeviceLayout.MeshSized`](@ref) for details on how points are used.
"""
function clear_mesh_control_points!()
    empty!(MESHSIZE_PARAMS[:cp])
    return empty!(MESHSIZE_PARAMS[:ct])
end

_stp_float(x::Length) = Float64(ustrip(STP_UNIT, x))
_stp_float(x::Real) = Float64(x)

const _MeshControlPointRecord = NTuple{5, Float64} # (x, y, z, h, α)
const _MeshControlPointOwners = Dict{Tuple{String, Int}, Set{_MeshControlPointRecord}}

# ─── Kernel-independent mesh-size control-point sampling ──────────────────────
#
# These tools compute mesh-size control points directly from rendered geometry
# primitives (the output of `to_primitives`), with NO geometry kernel involved.
# They are the kernel-agnostic replacement for sampling boundary points by
# querying the meshing backend (e.g. `gmsh.model.get_value` on fragmented OCC
# curve entities). Sampling at primitive level means the size field can be
# rebuilt from a `Schematic` alone (see `populate_size_fields!`) and is
# unit-testable without rendering through any `SolidModel`.
#
# Every sample is appended to `MESHSIZE_PARAMS[:cp]` under `(h, α)` via
# `add_mesh_size_point`. None of these call `finalize_size_fields!` — the
# caller finalizes once after all elements are processed so manual
# `add_mesh_size_point` additions and `α` changes can interleave first.

"""
    _collect_mesh_control_points!(prims, h, α, z;
        curvature_sizing=true, mesh_seen=nothing, mesh_points=nothing)

Append mesh-size control points for `prims` (a primitive or vector of
primitives from [`to_primitives`](@ref)) under `(h, α)` when `h > 0`, sampling each
primitive's boundary at `⌈L / h⌉` evenly-spaced arc-length intervals at
height `z`. When `curvature_sizing=true`, exact circular primitives also add a
radius-sized control point at their center, including when `h <= 0`. If
`mesh_points` is provided, all generated controls are also recorded for composition with
postrender extrusions. Does NOT finalize the size field.
"""
function _collect_mesh_control_points!(
    prims,
    h::Real,
    α::Real,
    z::Real;
    curvature_sizing::Bool=true,
    mesh_seen::Union{Nothing, Set{_MeshControlPointRecord}}=nothing,
    mesh_points::Union{Nothing, Set{_MeshControlPointRecord}}=nothing
)
    if h > 0
        h_float = Float64(h)
        α_float = Float64(α < 0 ? -1 : α)
        key = (h_float, α_float)
        record_points = !isnothing(mesh_seen) || !isnothing(mesh_points)
        first_new = record_points ? length(get(mesh_control_points(), key, ())) + 1 : 1
        _sample_meshsize!(prims, h_float, α_float, Float64(z))
        if record_points
            # `_sample_meshsize!` only creates `mesh_control_points()[key]` if it
            # actually emitted a sample; primitives that produce none
            # (e.g. sub-3-vertex loops) leave the key absent. Fall back to an
            # empty view so recording is a no-op then.
            new_points = get(mesh_control_points(), key, SVector{3, Float64}[])
            for point in (@view new_points[first_new:end])
                record = (point[1], point[2], point[3], h_float, α_float)
                !isnothing(mesh_seen) && push!(mesh_seen, record)
                !isnothing(mesh_points) && push!(mesh_points, record)
            end
        end
    end
    if curvature_sizing
        seen = isnothing(mesh_seen) ? Set{_MeshControlPointRecord}() : mesh_seen
        _sample_curvature_meshsize!(prims, Float64(z), seen, mesh_points)
    end
    return nothing
end

# Vector of primitives — fan out.
function _sample_meshsize!(prims::AbstractVector, h::Float64, α::Float64, z::Float64)
    return _sample_meshsize!.(prims, h, α, z)
end

# Polygon — walk consecutive vertex pairs as straight segments.
function _sample_meshsize!(p::AbstractPolygon, h::Float64, α::Float64, z::Float64)
    pts = points(p)
    n = length(pts)
    n >= 3 || return
    for i = 1:n
        _sample_straight_meshsize!(pts[i], pts[mod1(i + 1, n)], h, α, z)
    end
end

# CurvilinearPolygon — walk vertex pairs; if a `Paths.Segment` is recorded at
# this index, sample the segment with the per-`Paths.Segment` helper, otherwise
# treat the edge as an implicit straight line. The sign of `curve_start_idx`
# only encodes parametrisation direction relative to polygon traversal; the 3D
# sample locations are direction-independent so we ignore it.
function _sample_meshsize!(cp::CurvilinearPolygon, h::Float64, α::Float64, z::Float64)
    pts = cp.p
    n = length(pts)
    n >= 3 || return
    seg_at = Dict{Int, Paths.Segment}()
    for (k, csi) in enumerate(cp.curve_start_idx)
        seg_at[abs(csi)] = cp.curves[k]
    end
    for i = 1:n
        if haskey(seg_at, i)
            _sample_segment_meshsize!(seg_at[i], h, α, z)
        else
            _sample_straight_meshsize!(pts[i], pts[mod1(i + 1, n)], h, α, z)
        end
    end
end

# CurvilinearRegion — outer ring + each hole's perimeter.
function _sample_meshsize!(cr::CurvilinearRegion, h::Float64, α::Float64, z::Float64)
    _sample_meshsize!(cr.exterior, h, α, z)
    for hole in cr.holes
        _sample_meshsize!(hole, h, α, z)
    end
end

# Ellipse — sample its perimeter at h-spacing (Ramanujan perimeter estimate for
# the sample count; the parametric point evaluation is exact).
function _sample_meshsize!(e::Ellipse, h::Float64, α::Float64, z::Float64)
    a = ustrip(STP_UNIT, e.radii[1])
    b = ustrip(STP_UNIT, e.radii[2])
    perim = pi * (3 * (a + b) - sqrt((3a + b) * (a + 3b)))
    Ns = max(8, ceil(Int, perim / h))
    cx = ustrip(STP_UNIT, e.center.x)
    cy = ustrip(STP_UNIT, e.center.y)
    θ = ustrip(uconvert(°, e.angle)) * (π / 180)
    cs = cos(θ)
    sn = sin(θ)
    for i = 0:(Ns - 1)
        t = 2π * (i + 0.5) / Ns
        lx = a * cos(t)
        ly = b * sin(t)
        x = cx + cs * lx - sn * ly
        y = cy + sn * lx + cs * ly
        add_mesh_size_point(Float64[x, y, z]; h=h, α=α < 0 ? -1 : α)
    end
end

# Fallback: any primitive without a specific sampler contributes no points.
_sample_meshsize!(::Any, ::Float64, ::Float64, ::Float64) = nothing

# Curvature sizing is a separate primitive walk because otherwise-unsized curved entities
# still need a geometric resolution cap. For an exact circular arc of radius R, a control
# point at its center with h=R contributes exactly R on the arc when mesh_scale() <= 1,
# independently of the grading exponent.
function _sample_curvature_meshsize!(
    prims::AbstractVector,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    for prim in prims
        _sample_curvature_meshsize!(prim, z, seen, points)
    end
    return nothing
end

function _sample_curvature_meshsize!(
    cp::CurvilinearPolygon,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    for seg in cp.curves
        _sample_segment_curvature_meshsize!(seg, z, seen, points)
    end
    return nothing
end

function _sample_curvature_meshsize!(
    cr::CurvilinearRegion,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    _sample_curvature_meshsize!(cr.exterior, z, seen, points)
    for hole in cr.holes
        _sample_curvature_meshsize!(hole, z, seen, points)
    end
    return nothing
end

function _sample_curvature_meshsize!(
    e::Ellipse,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    iscircle(e) || return nothing
    return _add_curvature_mesh_size_point!(
        _stp_float(e.center.x),
        _stp_float(e.center.y),
        abs(_stp_float(e.radii[1])),
        z,
        seen,
        points
    )
end

function _sample_curvature_meshsize!(
    seg::Paths.Segment,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    return _sample_segment_curvature_meshsize!(seg, z, seen, points)
end

_sample_curvature_meshsize!(
    ::Any,
    ::Float64,
    ::Set{_MeshControlPointRecord},
    ::Union{Nothing, Set{_MeshControlPointRecord}}
) = nothing

# CompoundSegment in a CurvilinearPolygon is possible by manual construction
function _sample_segment_curvature_meshsize!(
    seg::Paths.CompoundSegment,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
)
    for subsegment in seg.segments
        _sample_segment_curvature_meshsize!(subsegment, z, seen, points)
    end
    return nothing
end

function _sample_segment_curvature_meshsize!(
    seg::Paths.ConstantOffset{T, S},
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
) where {T, S <: Paths.Turn{T}}
    return _sample_segment_curvature_meshsize!(Paths.resolve_offset(seg), z, seen, points)
end

function _sample_segment_curvature_meshsize!(
    seg::Paths.Turn{T},
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}
) where {T}
    center = Paths.curvaturecenter(seg)
    radius = abs(_stp_float(Paths.curvatureradius(seg, zero(T))))
    return _add_curvature_mesh_size_point!(
        _stp_float(center.x),
        _stp_float(center.y),
        radius,
        z,
        seen,
        points
    )
end

_sample_segment_curvature_meshsize!(
    ::Paths.Segment,
    ::Float64,
    ::Set{_MeshControlPointRecord},
    ::Union{Nothing, Set{_MeshControlPointRecord}}
) = nothing

function _add_composable_mesh_size_point!(
    x::Float64,
    y::Float64,
    z::Float64,
    h::Float64,
    α::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}=nothing
)
    all(isfinite, (x, y, z, h, α)) && h > 0 || return false
    record = (x, y, z, h, α < 0 ? -1.0 : α)
    !isnothing(points) && push!(points, record)
    record in seen && return false
    push!(seen, record)
    add_mesh_size_point(Float64[x, y, z]; h=h, α=α)
    return true
end

function _add_curvature_mesh_size_point!(
    x::Float64,
    y::Float64,
    radius::Float64,
    z::Float64,
    seen::Set{_MeshControlPointRecord},
    points::Union{Nothing, Set{_MeshControlPointRecord}}=nothing
)
    return _add_composable_mesh_size_point!(x, y, z, radius, -1.0, seen, points)
end

function _extrusion_z_offsets(dz::Float64, h::Float64, kwargs)
    # Explicit extrusion layers determine where the mesh needs controls. Without them, use
    # enough uniform intervals to keep every sidewall point within h of another control.
    options = (; kwargs...)
    num_elements = get(options, :num_elements, ())
    heights = get(options, :heights, ())
    if isempty(num_elements)
        intervals = max(1, ceil(Int, abs(dz) / h))
        return range(0.0, dz; length=intervals + 1)[2:end]
    end

    total_elements = sum(num_elements)
    total_elements > 0 || return Float64[]
    if isempty(heights)
        return range(0.0, dz; length=total_elements + 1)[2:end]
    end

    length(heights) == length(num_elements) ||
        throw(ArgumentError("heights and num_elements must have the same length"))
    normalized_offsets = Float64[]
    layer_start = 0.0
    for (elements, layer_end) in zip(num_elements, heights)
        elements > 0 || throw(ArgumentError("num_elements entries must be positive"))
        append!(
            normalized_offsets,
            range(layer_start, Float64(layer_end); length=elements + 1)[2:end]
        )
        layer_start = Float64(layer_end)
    end
    return dz .* normalized_offsets
end

function _compose_meshsize!(
    points_by_group::_MeshControlPointOwners,
    op,
    args,
    kwargs,
    seen::Set{_MeshControlPointRecord}
)
    # Only the built-in extrusion has the known point transformation needed here; arbitrary
    # postrender operations retain the existing primitive-coordinate sizing behavior.
    op === extrude_z! || return false
    length(args) >= 2 || return false
    groupdim = length(args) >= 3 ? Int(args[3]) : 2
    source_points = get(points_by_group, (string(args[1]), groupdim), nothing)
    isnothing(source_points) && return false
    dz = _stp_float(args[2])
    iszero(dz) && return false

    changed = false
    for (x, y, z, h, α) in source_points
        for z_offset in _extrusion_z_offsets(dz, h, kwargs)
            changed |= _add_composable_mesh_size_point!(x, y, z + z_offset, h, α, seen)
        end
    end
    return changed
end

# Generic `Paths.Segment` sampler. Every concrete `Paths.Segment` (Straight,
# Turn, BSpline, ConstantOffset, …) supports `pathlength(seg)` and `seg(s)`
# with `s` an arc-length parameter, so one uniform-in-s sampler at
# `Ns = ⌈L / h⌉` captures all curve types. Offset curves on Path/CPW
# boundaries are themselves `Paths.Segment` subtypes (e.g. `ConstantOffset`)
# by the time they reach here via `to_primitives`, so this covers the full
# CPW boundary without any per-style awareness.
function _sample_segment_meshsize!(seg::Paths.Segment, h::Float64, α::Float64, z::Float64)
    L = ustrip(STP_UNIT, pathlength(seg))
    L > 0 || return
    Ns = max(1, ceil(Int, L / h))
    L_native = pathlength(seg)
    for i = 0:(Ns - 1)
        pt = seg((i + 0.5) / Ns * L_native)
        add_mesh_size_point(
            Float64[ustrip(STP_UNIT, pt.x), ustrip(STP_UNIT, pt.y), z];
            h=h,
            α=α < 0 ? -1 : α
        )
    end
end

# `CompoundSegment` is a sequence of segments — walk each.
function _sample_segment_meshsize!(
    seg::Paths.CompoundSegment,
    h::Float64,
    α::Float64,
    z::Float64
)
    return _sample_segment_meshsize!.(seg.segments, h, α, z)
end

# Straight line between two points. Sample `Ns = ⌈L / h⌉` evenly-spaced points
# (interior, half-step offset so adjacent edges sample each shared corner from
# both sides symmetrically).
function _sample_straight_meshsize!(a::Point, b::Point, h::Float64, α::Float64, z::Float64)
    ax = ustrip(STP_UNIT, a.x)
    ay = ustrip(STP_UNIT, a.y)
    bx = ustrip(STP_UNIT, b.x)
    by = ustrip(STP_UNIT, b.y)
    dx = bx - ax
    dy = by - ay
    L = sqrt(dx * dx + dy * dy)
    L > 0 || return
    Ns = max(1, ceil(Int, L / h))
    for i = 0:(Ns - 1)
        t = (i + 0.5) / Ns
        add_mesh_size_point(Float64[ax + t * dx, ay + t * dy, z]; h=h, α=α < 0 ? -1 : α)
    end
end

"""
    reset_mesh_control!()

Reset the mesh scaling and grading to the original defaults: `(s_g, α) ← (1.0, 0.75)`.

See [`DeviceLayout.MeshSized`](@ref) for details and the explicit mesh sizing formula.
"""
function reset_mesh_control!()
    set_gmsh_option("Mesh.ElementOrder", 1)
    mesh_scale(1.0)
    return mesh_grading_default(0.75)
end

_default_size_primitives(node::Paths.Node; kwargs...) = to_polygons(node; kwargs...)
_default_size_primitives(el; kwargs...) = to_polygons(el; kwargs...)

"""
    populate_size_fields!(cs::AbstractCoordinateSystem; curvature_sizing=true, kwargs...) -> mesh_control_points()

Build the mesh-size control-point dictionary (`MESHSIZE_PARAMS[:cp]`) and its KDTrees
from `cs`, without querying a geometry kernel. For every flattened element with a positive
mesh size, rendered primitive boundaries are sampled at `⌈L / h⌉` arc-length intervals
and stored under `(h, α)`. Exact circular primitives can also contribute curvature-center
points independently of their entity mesh size.

This constructs the same data used by `render!`'s mesh-size callback, but can be called on
a coordinate system before rendering to a `SolidModel`.

Keywords:

  - `primitives_of = _default_size_primitives`: element-to-primitives function. `render!`
    uses `el -> to_primitives(sm, el)` so the sampled primitive form matches the model.
  - `zmap = (_) -> 0.0`: metadata-to-height function for the generated control points.
  - `curvature_sizing = true`: add radius-sized control points at the centers of exact
    circular primitives when `primitives_of` preserves them. Disable to retain perimeter-only
    sizing.
  - remaining keywords are forwarded to `sizeandgrading` and `primitives_of`.
"""
function populate_size_fields!(
    cs::AbstractCoordinateSystem;
    primitives_of=_default_size_primitives,
    zmap=(_) -> 0.0,
    curvature_sizing=true,
    kwargs...
)
    flat = flatten(cs)
    clear_mesh_control_points!()
    mesh_seen = Set{_MeshControlPointRecord}()
    for (el, meta) in zip(elements(flat), element_metadata(flat))
        h, α = sizeandgrading(el; kwargs...)
        h > 0 || curvature_sizing || continue
        _collect_mesh_control_points!(
            primitives_of(el; kwargs...),
            h,
            α,
            _stp_float(zmap(meta));
            curvature_sizing,
            mesh_seen
        )
    end
    finalize_size_fields!()
    return mesh_control_points()
end

"""
    Base.@kwdef struct MeshingParameters
        mesh_scale::Float64 = 1.0
        mesh_order::Int = 1
        α_default::Float64 = 0.75
        apply_size_to_surfaces::Bool = false
        high_order_optimize::Int = 1
        surface_mesh_algorithm::Int = 6
        volume_mesh_algorithm::Int = 1
        options::Dict{String, Union{String, Float64}} = Dict{String, Union{String, Float64}}()
    end

!!! warning "Deprecated"

    This struct is deprecated. See [`render!`](@ref)

MeshingParameters contains high level parameters to specify mesh sizing
fields throughout the domain.

  - `mesh_scale` applies multiplicatively to the smallest size specified by any size field
    function, comparing to the formula in the `MeshSized` style, this results in all mesh size
    fields being rescaled where `h` ← `mesh_scale` * `h`.
  - `mesh_order` specifies the order of polynomials to use in representing the geometry, this
    is important if curved geometric features are present, `mesh_order == 1` will represent
    the geometry with linear polynomials, whilst `mesh_order == 2` will represent it with
    quadratic polynomials, and `mesh_order == 3` with cubic polynomials. Increasing the value
    of `mesh_order` results in greater geometric fidelity, whilst making meshing more
    difficult (and prone to errors).
  - `α_default` specifies the default value of `α` to use for `MeshSized` entities where `α`
    is set to less than 0, `α_default ∈ (0, 1]` is particularly used for the default grading
    of `Path` entities. A value closer to 1 can result in an unstable meshing algorithm in gmsh,
    particularly for complex geometries.
  - `apply_size_to_surfaces=true` will cause the mesh sizing field to specify the size within
    any sized entities, as opposed to only along the perimeter of the entity if
    `apply_size_to_surfaces=false`. Setting `apply_size_to_surfaces=true` will result in a
    larger number of elements.
  - `high_order_optimize=1` flag to pass to gmsh if optimization of a higher order mesh is
    to be performed. (0: none, 1: optimization, 2: elastic+optimization, 3: elastic, 4: fast
    curving). Refer to the gmsh documentation for more details.
  - `surface_mesh_algorithm` specifies the algorithm gmsh should use when performing the
    surface mesh generation. Refer to the gmsh documentation for more details.
  - `volume_mesh_algorithm` specifies the algorithm gmsh should use when performing the
    volume mesh generation. Refer to the gmsh documentation for more details.
  - `options` used to specify any additional options provided to gmsh, which will be set with
    `gmsh.options.set_number(key, value)` for each `key => value` pair. Refer to the gmsh
    documentation for a list of available options. Will override any other options as is
    called last.
"""
Base.@kwdef struct MeshingParameters
    mesh_scale::Float64 = 1.0
    mesh_order::Int = 1
    α_default::Float64 = 0.75
    apply_size_to_surfaces::Bool = false
    high_order_optimize::Int = 1
    surface_mesh_algorithm::Int = 6
    volume_mesh_algorithm::Int = 1
    options::Dict{String, Union{String, Float64}} = Dict{String, Union{String, Float64}}()
end

"""
    render!(sm::SolidModel, cs::AbstractCoordinateSystem{T}; map_meta=layer,
    postrender_ops=[], zmap=(_) -> zero(T), gmsh_options = Dict(), skip_postrender = false,
    auto_union=false, skip_unused_layers=false, curvature_sizing=true, kwargs...) where {T}

Render `cs` to `sm`.

# Keywords

  - `map_meta`: Function (m::SemanticMeta) -> name of `PhysicalGroup` (as `String` or `Symbol`; may also return `nothing` to skip rendering `m`)
  - `postrender_ops`: Vector of Tuples `(destination, op, args, op_kwargs...)` specifying "postrendering"
    of `PhysicalGroup`s executed after entities have been rendered to to `sm`.
    Each operation `op` creates a new `PhysicalGroup` defined as
    `sm[destination] = op(sm, args...; op_kwargs...)`. That is, `args` are the arguments to
    `op` (following the first argument, which is always the model `sm` being rendered to).
    For most operations, these arguments include the names and dimensions of groups being
    operated on, and `op_kwargs` are the keyword arguments passed to `op`. For example,
    `("base", difference_geom!, ("writeable_area", "base_negative"), :remove_object => true, :remove_tool => true)`
    defines a postrendering step that subtracts the `PhysicalGroup` named `"base_negative"`
    from `"writeable_area"` (by default using dimension 2 for each group) to define a new group called
    `"base"`. The keyword pairs `:remove_object=>true` and `:remove_tool=>true` mean
    that the "object" (first argument) group `"writeable_area"` and the "tool" (second argument)
    group `"base_negative"` are both removed when `"base"` is created.
  - `retained_physical_groups`: Vector of `(name, dimension)` tuples specifying which physical groups to keep after rendering. All other groups are removed.
  - `zmap`: Function (m::SemanticMeta) -> `z` coordinate of corresponding elements. Default:
    Map all metadata to zero.
  - `gmsh_options`: Dictionary of gmsh option name-value pairs to set before meshing.
  - `meshing_parameters`: **Deprecated.** Use individual mesh control functions
    [`DeviceLayout.SolidModels.mesh_scale`](@ref), [`DeviceLayout.SolidModels.mesh_order`](@ref) and [`DeviceLayout.SolidModels.mesh_grading_default`](@ref), along with
    `gmsh_options` instead.
  - `skip_postrender`: Whether or not to return early without performing any postrendering
    operations. This can be particularly helpful during debugging, as all two dimensional
    entities will be placed appropriately but will not have been combined.
  - `auto_union`: If `true`, union each 2D physical group
    as the first postrender step, before extrusions and user-defined `postrender_ops`. This
    consolidates overlapping entities within each group, reducing the cost of subsequent
    pairwise fragmentation. Default is `false`.
  - `skip_unused_layers`: If `true`, skip rendering layers whose names are not referenced by
    `postrender_ops` or `retained_physical_groups`. A layer is considered referenced if either
    its mapped name or its base layer name (from `layer(meta)`) appears in the referenced set.
    This keeps indexed and levelwise variants (e.g. `"port_1"`) when the base layer (`"port"`)
    is referenced. Default is `false`.
  - `curvature_sizing`: If `true`, add radius-sized mesh control points at the centers of exact
    circular primitives preserved by the rendering backend. For `extrude_z!` postrender
    operations, generated perimeter and curvature controls are repeated at requested extrusion
    mesh layers, or at intervals no larger than each control's target size when no layers are
    specified. Default is `true`.

Available postrendering operations include [`translate!`](@ref), [`extrude_z!`](@ref), [`revolve!`](@ref),
[`union_geom!`](@ref), [`intersect_geom!`](@ref), [`difference_geom!`](@ref), [`fragment_geom!`](@ref), and [`box_selection`](@ref).
(The geometric Boolean operations are only available for models using the OpenCASCADE kernel.)

Additional keyword arguments are passed to [`DeviceLayout.SolidModels.to_primitives`](@ref) (which falls back to
[`to_polygons`](@ref)) and may be used for
certain entity types to control how entities of `cs` are converted to primitives and added to `sm`.
"""
function render!(
    sm::SolidModel,
    cs::AbstractCoordinateSystem{T};
    map_meta=layer,
    postrender_ops=[],
    retained_physical_groups=[],
    zmap=(_) -> zero(T),
    gmsh_options=Dict{String, Union{String, Int, Float64}}(),
    meshing_parameters::Union{Nothing, MeshingParameters}=nothing,
    skip_postrender=false,
    auto_union=false,
    skip_unused_layers=false,
    curvature_sizing=true,
    kwargs...
) where {T}
    return _render_orchestrator!(
        sm,
        cs;
        (emit!)=(els, meta, k; zmap, points_cache, kwargs...) ->
            _add_to_current_solidmodel!(
                els,
                meta,
                k;
                zmap=zmap,
                points_cache=points_cache,
                kwargs...
            ),
        (fragment!)=_fragment_three_pass!,
        map_meta=map_meta,
        postrender_ops=postrender_ops,
        retained_physical_groups=retained_physical_groups,
        zmap=zmap,
        gmsh_options=gmsh_options,
        meshing_parameters=meshing_parameters,
        skip_postrender=skip_postrender,
        auto_union=auto_union,
        skip_unused_layers=skip_unused_layers,
        curvature_sizing=curvature_sizing,
        kwargs...
    )
end

# Adjacent-dimension pairs avoid both exterior boundary loss ([3,2,1], PR #145)
# and stale OCC bindings when combined ([1,2,3], Gmsh #3446 / issue #172).
function _fragment_three_pass!(sm::SolidModel)
    _fragment_and_map!(sm, [0, 1])
    _fragment_and_map!(sm, [1, 2])
    _fragment_and_map!(sm, [2, 3])
    return sm
end

# Shared orchestrator body called by both `render!` and `render_conformal!`.
# The two entry points differ only in:
#   - `emit!(els, meta, k; zmap, points_cache, kwargs...)`: how OCC entities are
#     added for a metadata group. Stock uses `_add_to_current_solidmodel!`;
#     conformal wraps `_add_conformal!` closing over the caller's context.
#   - `fragment!(sm)`: post-postrender fragment pass. Stock always runs the
#     three-pass `_fragment_and_map!`; conformal only runs it when
#     `fragment_backstop=true`.
# Every other step (gmsh setup, metadata loop, control-point collection,
# postrender, retained-group cleanup, final synchronize) is common.
function _render_orchestrator!(
    sm::SolidModel,
    cs::AbstractCoordinateSystem{T};
    emit!,
    fragment!,
    map_meta=layer,
    postrender_ops=[],
    retained_physical_groups=[],
    zmap=(_) -> zero(T),
    gmsh_options=Dict{String, Union{String, Int, Float64}}(),
    meshing_parameters::Union{Nothing, MeshingParameters}=nothing,
    skip_postrender=false,
    auto_union=false,
    skip_unused_layers=false,
    curvature_sizing=true,
    kwargs...
) where {T}
    gmsh.model.set_current(name(sm))

    if !isnothing(meshing_parameters)
        msg = "The `meshing_parameters` keyword is deprecated. Use the individual mesh control functions `SolidModels.mesh_scale`, `SolidModels.mesh_order`, and `SolidModels.mesh_grading_default`, along with `gmsh_options`, instead"
        if meshing_parameters.apply_size_to_surfaces
            msg *= "; `apply_size_to_surfaces` has no effect and should be removed"
        end
        Base.depwarn(msg, :render!)
        mesh_scale(meshing_parameters.mesh_scale)
        mesh_order(meshing_parameters.mesh_order, meshing_parameters.high_order_optimize)
        mesh_grading_default(meshing_parameters.α_default)
        gmsh_options["Mesh.Algorithm"] = meshing_parameters.surface_mesh_algorithm
        gmsh_options["Mesh.Algorithm3D"] = meshing_parameters.volume_mesh_algorithm
        merge!(gmsh_options, meshing_parameters.options)
    end

    set_gmsh_option(gmsh_options)

    flat = flatten(cs)

    clear_mesh_control_points!()
    mesh_seen = Set{_MeshControlPointRecord}()
    mesh_points_by_group = _MeshControlPointOwners()

    # Point-dedup cache: R-tree of previously inserted OCC dim-0 entities,
    # plus a set of live tags kept in sync with OCC's set via a stale flag.
    points_cache = PointsCache()

    # Build set of used layer names for skip_unused_layers optimization
    used_names = if skip_unused_layers
        _used_group_names(postrender_ops, retained_physical_groups)
    else
        nothing
    end

    # Create physical groups
    for meta in unique(element_metadata(flat)) # For each unique (layer, level, index) triple
        mapped_name = map_meta(meta)
        isnothing(mapped_name) && continue
        if !isnothing(used_names) &&
           string(mapped_name) ∉ used_names &&
           string(layer(meta)) ∉ used_names
            continue
        end
        idx = (element_metadata(flat) .== meta) # Get the corresponding elements
        els = to_primitives.(sm, elements(flat)[idx]; kwargs...)
        meshsizes = sizeandgrading.(elements(flat)[idx]; kwargs...)

        # Add to model using kernel via the strategy-specific emit function.
        group_dimtags_unflattened =
            emit!(els, meta, kernel(sm); zmap=zmap, points_cache=points_cache, kwargs...)

        group_dimtags = reduce(vcat, group_dimtags_unflattened, init=Tuple{Int32, Int32}[])
        # If group already exists, add to it
        for dim in unique(first.(group_dimtags))
            if hasgroup(sm, mapped_name, dim)
                append!(group_dimtags, dimtags(sm[mapped_name, dim]))
            end
        end

        # Make physical group for each dimension
        sm[mapped_name] = group_dimtags

        # Sample mesh size control points from the same primitive form added to the model.
        z_of_meta = _stp_float(zmap(meta))
        for (prims, (h, α), dts) in zip(els, meshsizes, group_dimtags_unflattened)
            mesh_points = Set{_MeshControlPointRecord}()
            _collect_mesh_control_points!(
                prims,
                h,
                α,
                z_of_meta;
                curvature_sizing,
                mesh_seen,
                mesh_points
            )
            isempty(mesh_points) && continue
            primitive_dimtags = dts isa Vector ? dts : [dts]
            for dim in unique(first.(primitive_dimtags))
                union!(
                    get!(
                        mesh_points_by_group,
                        (string(mapped_name), Int(dim)),
                        Set{_MeshControlPointRecord}()
                    ),
                    mesh_points
                )
            end
        end
    end
    # Synchronize the entities to the model.
    _synchronize!(sm)

    # Generate the KDTrees corresponding to the meshing control points.
    finalize_size_fields!()
    # Extrusions, Booleans, etc
    _synchronize!(sm)
    skip_postrender && return nothing
    # Union each physical group to consolidate overlapping entities before postrender.
    # Doing this before extrusions/booleans reduces the cost of pairwise fragmentation.
    if auto_union
        auto_union_ops = Tuple[]
        for groupname in collect(keys(dimgroupdict(sm, 2))) # Only dim 2 will be present
            push!(auto_union_ops, (groupname, union_geom!, (groupname, 2)))
        end
        _postrender!(sm, auto_union_ops)
        _synchronize!(sm)
    end
    meshsize_composed = _postrender!(sm, postrender_ops; mesh_points_by_group, mesh_seen)
    _synchronize!(sm)
    # Get rid of redundant entities and update groups accordingly.
    fragment!(sm)

    # Rebuild KDTrees to include mesh-size controls composed with extrusions.
    meshsize_composed && finalize_size_fields!()

    # Pass in call back function for meshing against the vertices found previously.
    gmsh.model.mesh.setSizeCallback(gmsh_meshsize)

    # Remove all physical groups except those on the retained list.
    if !isempty(retained_physical_groups)
        for d ∈ 0:3
            retain_groups = getindex.(filter(x -> x[2] == d, retained_physical_groups), 1)
            all_groups = keys(dimgroupdict(sm, d))
            setdiff(all_groups, retain_groups)
            for k ∈ setdiff(all_groups, retain_groups)
                remove_group!(sm[k, d], remove_entities=false)
            end
        end

        # Reindex the physical groups to improve human readability
        reindex_physical_groups!(sm)
    end

    return _synchronize!(sm)
end

"""
    gmsh_meshsize(dim::Cint, tag::Cint, x::Cdouble, y::Cdouble, z::Cdouble, lc::Cdouble) -> Float64

Gmsh callback function for adaptive mesh sizing based on distance to control points.

Computes mesh element size at point `(x, y, z)` using distance-based scaling from
control points stored in global `MESHSIZE_PARAMS[:ct]`. For each control point set
with parameters `(h, α)`, calculates size as `h * max(mesh_scale, (d/h)^α)` where
`d` is distance to nearest control point, using formula expressed in
[`DeviceLayout.MeshSized`](@ref).

# Arguments

  - `dim::Cint`: Entity dimension (unused)
  - `tag::Cint`: Entity tag (unused)
  - `x::Cdouble`: X coordinate
  - `y::Cdouble`: Y coordinate
  - `z::Cdouble`: Z coordinate
  - `lc::Cdouble`: Characteristic length (unused)

# Returns

  - `Float64`: Minimum computed mesh size across all control point sets

# Notes

Uses global `MESHSIZE_PARAMS` to avoid LLVM closure limitations on Apple Silicon.
Requires `MESHSIZE_PARAMS[:ct]` and `MESHSIZE_PARAMS[:mesh_scale]` to be set.
"""
function gmsh_meshsize(
    dim::Cint,
    tag::Cint,
    x::Cdouble,
    y::Cdouble,
    z::Cdouble,
    lc::Cdouble
)
    l = Inf64
    for ((h, α), tree) in mesh_control_trees()
        _, d::Float64 = nn(tree, SVector{3}(x, y, z))
        l = min(l, h * max(mesh_scale(), (d / h)^α))::Float64
    end
    return l
end

# Utility intended for very last step in rendering, to get rid of overlapping geometry
# All groups will point to the same volume/area/etc, but tags, entity count, etc may change.
# frag_dims specifies the dimensions to be included in fragmentation, whilst
# excluded_physical_groups are physical groups not to be included in the fragmentation.
function _fragment_and_map!(
    sm::SolidModel,
    frag_dims;
    excluded_physical_groups=PhysicalGroup[]
)
    gmsh.model.set_current(name(sm))
    # Get the tags of entities in existing groups
    groups = [
        (name, dimtags(pg)) for dim in frag_dims for
        (name, pg) in pairs(dimgroupdict(sm, dim))
    ]
    allents = vcat([gmsh.model.get_entities(dim) for dim in frag_dims]...)

    # Remove any excluded groups from the fragment.
    if !isempty(excluded_physical_groups)
        allents = setdiff(allents, [dimtags(pg) for pg in excluded_physical_groups]...)
        groups =
            setdiff(groups, [(name(pg), dimtags(pg)) for pg ∈ excluded_physical_groups])
    end

    # Fragment will preserve tags if possible
    # but otherwise will remove entities and create new ones
    if true
        frags, entmap = kernel(sm).fragment(allents, [])
    else
        # Manual fragment map construction for debugging purposes.
        frags, _ = kernel(sm).fragment(allents, [], -1, false, false)

        # Each returned fragment will be subset of at least one incoming entity.
        # TODO: Speed this up by exploiting that an entity that maps to itself will never be
        # found again, so can be removed from a copy of the origin entities.
        function collect_entity_map(entity, origin_entities, exact_matches)
            e = [entity]
            m = Tuple{Int32, Int32}[]
            # Filter to only look for entities of the same dimensionality, not already found
            for o ∈ setdiff(filter(x -> x[1] == entity[1], origin_entities), exact_matches)
                if entity == o
                    @assert isempty(m)
                    # An exact match cannot map onto any other entity, so add to the filter.
                    push!(exact_matches, o)
                    return e # exact match don't need to keep searching.
                end
                i, _ = kernel(sm).intersect(e, [o], -1, false, false)
                if !isempty(i)
                    # If the intersection is non empty, count it.
                    push!(m, o)
                    if i[1] != entity && i[1] != o
                        # If the intersection is neither input, the newly created entry
                        # should be removed.
                        kernel(sm).remove(i)
                    end
                end
            end
            return m
        end
        # Buffer to collect entities that were fragments when input. Once the exact map is
        # discovered, do not include in the search on remaining outputs.
        incoming_fragments = Tuple{Int32, Int32}[]
        entmap = collect_entity_map.(allents, Ref(frags), Ref(incoming_fragments))

        # Remove all entities that are not also output fragments.
        kernel(sm).remove(setdiff(allents, frags))
    end
    isempty(entmap) && return _synchronize!(sm)
    # For each original group,
    # reassign the group to the fragments its elements were mapped to
    for (name, dim_tags) in groups
        isempty(dim_tags) && continue
        sm[name] = vcat((entmap[indexin(dim_tags, allents)])...)
    end
    return _synchronize!(sm)
end
# GmshNative has no fragment
function _fragment_and_map!(
    ::SolidModel{GmshNative},
    frag_dims;
    excluded_physical_groups=PhysicalGroup[]
) end

# Assumes `gmsh` has been initialized and the current model has been set beforehand, and that
# the model will be synchronized afterward.
function _add_to_current_solidmodel! end

# render! broadcasts this over vectors of vectors, and it broadcasts itself over vectors...
function _add_to_current_solidmodel!(els, m::Meta, k; kwargs...)
    return _add_to_current_solidmodel!.(els, m, k; kwargs...)
end

_get_boundary_points(dt::Tuple) = _get_boundary_points([dt])
function _get_boundary_points(dts)
    ents = gmsh.model.get_boundary(dts, false, true, true) # not combined, oriented, recursive
    return filter(ent -> iszero(first(ent)), ents)
end

const POINT_MERGE_ATOL = 1e-9 # in STP_UNIT, i.e. atol=1e-6nm

"""
    PointsCache

Orchestrator-local state for point deduplication during a `render!` /
`render_conformal!` pass. Bundles together:

  - `tree` — an `RTree` of previously inserted OpenCASCADE dim-0 entities,
    keyed by coordinate.
  - `live` — the set of dim-0 tags known to still be live in OCC. Populated
    on every insert.
  - `stale` — set to `true` after any boolean op that can retag or delete
    dim-0 entities (currently only `k.cut` inside
    `_add_to_current_solidmodel!(::CurvilinearRegion)`), and reset to
    `false` by `_refresh_live_points!` the next time a liveness check
    needs to know.

`_render_orchestrator!` creates one `PointsCache` per pass and threads it
through the emit-callback into `_add_to_current_solidmodel!` /
`_add_conformal!`.
"""
mutable struct PointsCache
    tree::RTree{Float64, 3, SpatialIndexing.SpatialElem{Float64, 3, Nothing, Int32}}
    live::Set{Int32}
    stale::Bool
end
PointsCache() = PointsCache(RTree{Float64, 3}(Int32), Set{Int32}(), true)

function _get_or_add_points!(k, pts_xy, z, points_cache; atol=POINT_MERGE_ATOL)
    return _get_or_add_point!.(
        k,
        getx.(pts_xy),
        gety.(pts_xy),
        z,
        Ref(points_cache);
        atol=atol
    )
end

function _get_or_add_point!(
    k,
    x::Length,
    y::Length,
    z::Length,
    points_cache;
    atol=POINT_MERGE_ATOL
)
    return _get_or_add_point!(
        k,
        float(ustrip(STP_UNIT, x)),
        float(ustrip(STP_UNIT, y)),
        float(ustrip(STP_UNIT, z)),
        points_cache,
        atol=atol
    )
end

function _get_or_add_point!(
    k,
    x::Float64,
    y::Float64,
    z::Float64,
    points_cache::Nothing;
    atol=POINT_MERGE_ATOL
)
    return k.add_point(x, y, z)
end

function _get_or_add_point!(
    k::GmshNative,
    x::Float64,
    y::Float64,
    z::Float64,
    points_cache::PointsCache;
    atol=POINT_MERGE_ATOL
)
    return k.add_point(x, y, z)
end

function _get_or_add_point!(
    k,
    x::Float64,
    y::Float64,
    z::Float64,
    points_cache::PointsCache;
    atol=POINT_MERGE_ATOL
)
    reg =
        SpatialIndexing.Rect((x - atol, y - atol, z - atol), (x + atol, y + atol, z + atol))
    while true
        pts = SpatialIndexing.contained_in(points_cache.tree, reg)
        if isempty(pts)
            tag = k.add_point(x, y, z)
            insert!(points_cache.tree, SpatialIndexing.Point((x, y, z)), tag)
            push!(points_cache.live, tag)
            return tag
        end
        # If multiple candidate points fall in the tolerance box, tie-break
        # deterministically by (distance, tag) — R-tree iteration order is
        # not guaranteed deterministic across builds/versions, so we can't
        # rely on `first(pts)` alone.
        E = eltype(pts)
        best_elem = nothing
        best_d2 = Inf
        for pt::E in pts
            if isnothing(best_elem)
                best_elem = pt
                best_d2 = _point_d2(x, y, z, best_elem.mbr.low)
                continue
            end
            d2 = _point_d2(x, y, z, pt.mbr.low)
            if (d2 < best_d2) || (d2 == best_d2 && pt.val < best_elem.val)
                best_d2 = d2
                best_elem = pt
            end
        end
        # Verify the tag still refers to a live OpenCASCADE dim-0 entity.
        # Boolean ops (e.g. `k.cut`) can retag or delete entities after we
        # inserted them; if the tag is stale we drop it and retry.
        if _is_live_point(k, points_cache, best_elem.val)
            return best_elem.val
        end
        delete!(points_cache.tree, best_elem.mbr)
    end
end

# Squared distance from (x, y, z) to a SpatialIndexing point-Rect (low == high).
@inline function _point_d2(x, y, z, c)
    dx = x - c[1]
    dy = y - c[2]
    dz = z - c[3]
    return dx * dx + dy * dy + dz * dz
end

function _refresh_live_points!(k, cache::PointsCache)
    empty!(cache.live)
    union!(cache.live, Iterators.map(last, k.get_entities(0)))
    cache.stale = false
    return nothing
end

_mark_points_stale!(cache::PointsCache) = (cache.stale = true; nothing)
_mark_points_stale!(::Nothing) = nothing

function _is_live_point(k, cache::PointsCache, tag)
    cache.stale && _refresh_live_points!(k, cache)
    return tag in cache.live
end

# Add primitives to solid model
function _add_to_current_solidmodel!(
    poly::AbstractPolygon{T},
    m::Meta,
    k;
    zmap=(_) -> zero(T),
    points_cache=nothing,
    atol=DeviceLayout.onenanometer(T),
    kwargs...
) where {T}
    z = zmap(m) # map from m using kwargs
    # Add as curve loop to get point deduplication
    loop = _add_loop!(CurvilinearPolygon(points(poly)), k, z; points_cache, atol)
    surf = k.add_plane_surface([loop])

    return (Int32(2), surf)
end

_add_to_current_solidmodel!(
    x::CurvilinearPolygon,
    m::Meta,
    k;
    zmap=(_) -> zero(T),
    points_cache=nothing,
    kwargs...
) = _add_to_current_solidmodel!(CurvilinearRegion(x), m, k; zmap, points_cache, kwargs...)

# GmshNative flattens curvilinear primitives in `to_primitives`, so this sink is
# OpenCascade-only in normal rendering.
function _add_to_current_solidmodel!(
    surf::CurvilinearRegion{T},
    m::Meta,
    k;
    zmap=(_) -> zero(T),
    points_cache=nothing,
    atol=DeviceLayout.onenanometer(T),
    kwargs...
) where {T}
    z = zmap(m) # map from m using kwargs
    outer_loop = _add_loop!(surf.exterior, k, z; points_cache, atol)
    hole_loops = _add_loop!.(surf.holes, k, z; points_cache, atol)

    surftag = k.add_plane_surface([outer_loop])
    if !isempty(hole_loops)
        holes = [k.add_plane_surface([h]) for h ∈ hole_loops]
        out_dim_tags, _ = k.cut([(2, surftag)], [(2, x) for x ∈ holes])
        surftag = out_dim_tags[1][2]
        # `k.cut` may have retagged or deleted dim-0 entities referenced by
        # `points_cache`; force the next liveness probe to refresh.
        _mark_points_stale!(points_cache)
    end
    return (Int32(2), surftag)
end

function _add_to_current_solidmodel!(
    line::LineSegment{T},
    m::Meta,
    k;
    zmap=(_) -> zero(T),
    points_cache=nothing,
    atol=DeviceLayout.onenanometer(T),
    kwargs...
) where {T}
    z = zmap(m)
    p0 = _get_or_add_point!(k, getx(line.p0), gety(line.p0), z, points_cache)
    p1 = _get_or_add_point!(k, getx(line.p1), gety(line.p1), z, points_cache)
    linetag = k.add_line(p0, p1)
    return (Int32(1), linetag)
end

# Sub-primitive methods for loops and curves
function _add_loop!(
    cl::CurvilinearPolygon,
    k,
    z;
    points_cache=nothing,
    atol=DeviceLayout.onenanometer(coordinatetype(cl))
)
    pts = _get_or_add_points!(k, points(cl), z, points_cache)
    endpoint_pairs = zip(pts, circshift(pts, -1))
    curves = map(enumerate(endpoint_pairs)) do (i, endpoints)
        curve_idx = findfirst(isequal(i), cl.curve_start_idx)
        if isnothing(curve_idx)
            return k.add_line(endpoints[1], endpoints[2])
        else
            return _add_curve!(endpoints, cl.curves[curve_idx], k, z; atol)
        end
    end
    return k.add_curve_loop(collect(Iterators.flatten(curves)))
end

# Exact circular arc
function _add_curve!(endpoints, seg::Paths.Turn, k::OpenCascade, z; kwargs...)
    center_pt =
        seg.p0 + Point(-seg.r * sign(seg.α)sin(seg.α0), seg.r * sign(seg.α)cos(seg.α0))
    cen = k.add_point(
        ustrip(STP_UNIT, getx(center_pt)),
        ustrip(STP_UNIT, gety(center_pt)),
        ustrip(STP_UNIT, z)
    )
    # Split the arc in 2 if necessary
    if abs(seg.α) >= 180° # Arcs have to be strictly less than pi
        n_180 = abs(seg.α) / 180°
        n_arcs = if ceil(n_180) == n_180
            Int(n_180 + 1)
        else
            Int(ceil(n_180))
        end
        arclengths = range(zero(pathlength(seg)), pathlength(seg), length=n_arcs + 1)
        middle_pts = seg.(arclengths[(begin + 1):(end - 1)])
        middle_tags =
            k.add_point.(
                ustrip.(STP_UNIT, getx.(middle_pts)),
                ustrip.(STP_UNIT, gety.(middle_pts)),
                ustrip(STP_UNIT, z)
            )
        tags = [endpoints[1]; middle_tags; endpoints[2]]
        return k.add_circle_arc.(tags[1:(end - 1)], cen, tags[2:end], -1)
    end

    try
        return k.add_circle_arc(endpoints[1], cen, endpoints[2], -1)
    catch e
        if e isa ErrorException && contains(e.msg, "Could not create circle arc")
            @debug "addCircleArc failed, falling back to line" p0 = seg.p0 r = seg.r α =
                seg.α α0 = seg.α0
            return k.add_line(endpoints[1], endpoints[2])
        end
        rethrow()
    end
end

# Exact *interpolating* cubic BSpline in OCC
# (occ.addBSpline and geo.addBSpline instead use control points, and geo.addSpline uses Catmull-Rom splines)
function _add_curve!(endpoints, seg::Paths.BSpline, k::OpenCascade, z; kwargs...)
    midpts =
        k.add_point.(
            ustrip.(STP_UNIT, getx.(seg.p[2:(end - 1)])),
            ustrip.(STP_UNIT, gety.(seg.p[2:(end - 1)])),
            ustrip(STP_UNIT, z)
        )
    pts = [endpoints[1], midpts..., endpoints[2]]
    # Tangents for start and end as concatenated 3d vectors
    tangents = [
        ustrip(STP_UNIT, seg.t0.x),
        ustrip(STP_UNIT, seg.t0.y),
        0,
        ustrip(STP_UNIT, seg.t1.x),
        ustrip(STP_UNIT, seg.t1.y),
        0
    ]
    return k.addSpline( # C2 B-spline that goes through pts
        pts,
        -1, # just use next tag
        tangents
    )
end

# Offset curves
_add_curve!(endpoints, seg::Paths.OffsetSegment, k, z; kwargs...) =
    _add_offset_curve!(endpoints, seg.seg, seg.offset, k, z; kwargs...)
# Turns with constant offsets are still circular arcs
function _add_offset_curve!(endpoints, seg::Paths.Turn, offset::Coordinate, k, z; kwargs...)
    return _add_curve!(
        endpoints,
        Paths.Turn(
            seg.α,
            seg.r - sign(seg.α) * offset,
            seg.p0 + Point(-sin(seg.α0), cos(seg.α0)) * offset,
            seg.α0
        ),
        k,
        z
    )
end

# Any other offset curve (BSpline or variable offset) will be approximated by a BSpline
function _add_offset_curve!(
    endpoints,
    seg::Paths.Segment,
    offset,
    k,
    z;
    atol=DeviceLayout.onenanometer(typeof(offset))
)
    bspline_approx = bspline_approximation(Paths.offset(seg, offset); atol)
    newstarts = DeviceLayout.p0.(bspline_approx.segments)[2:end]
    newpts =
        k.add_point.(
            ustrip.(STP_UNIT, getx.(newstarts)),
            ustrip.(STP_UNIT, gety.(newstarts)),
            ustrip(STP_UNIT, z)
        )
    starts = [first(endpoints), newpts...]
    stops = [newpts..., last(endpoints)]
    endp_pairs = [[start, stop] for (start, stop) in zip(starts, stops)]
    return _add_curve!.(endp_pairs, bspline_approx.segments, k, z)
end

"""
    _used_group_names(postrender_ops, retained_physical_groups)

Build a `Set{String}` of physical group names referenced by `postrender_ops` or
`retained_physical_groups`. Used by `skip_unused_layers` to avoid rendering
entities for unreferenced layers.
"""
function _used_group_names(postrender_ops, retained_physical_groups)
    names = Set{String}()
    for (name, _) in retained_physical_groups
        push!(names, string(name))
    end
    for op in postrender_ops
        # op = (destination, func, args, kwargs...)
        push!(names, string(op[1]))  # destination name
        if length(op) >= 3
            _extract_op_names!(names, op[3])  # args tuple (kwargs are never layer names)
        end
    end
    return names
end

# Recursively extract String and Symbol values from nested args structures.
# Numeric parameters (dimensions, thicknesses) are Int or length-unit types, never strings.
_extract_op_names!(names::Set{String}, x::Union{String, Symbol}) = push!(names, string(x))
_extract_op_names!(names::Set{String}, x::Union{Tuple, AbstractVector}) =
    foreach(a -> _extract_op_names!(names, a), x)
_extract_op_names!(names::Set{String}, ::Any) = nothing
